#include "../../../ray.conf"
#include "../../../ray_advanced.conf"
#include "../../../shader/math.fxsub"
#include "../../../shader/common.fxsub"
#include "../../../shader/shadowcommon.fxsub"
#include "../../../shader/gbuffer.fxsub"
#include "../../../shader/gbuffer_sampler.fxsub"
#include "../../../shader/PhaseFunctions.fxsub"

float mR : CONTROLOBJECT<string name="(self)"; string item = "R+";>;
float mG : CONTROLOBJECT<string name="(self)"; string item = "G+";>;
float mB : CONTROLOBJECT<string name="(self)"; string item = "B+";>;
float mRangeP : CONTROLOBJECT<string name="(self)"; string item = "Range+";>;
float mRangeM : CONTROLOBJECT<string name="(self)"; string item = "Range-";>;
float mIntensityP : CONTROLOBJECT<string name="(self)"; string item = "Intensity+";>;
float mIntensityM : CONTROLOBJECT<string name="(self)"; string item = "Intensity-";>;
float mAngleP : CONTROLOBJECT<string name = "(self)"; string item = "Angle+";>;
float mAngleM : CONTROLOBJECT<string name = "(self)"; string item = "Angle-";>;
float mBlink : CONTROLOBJECT<string name="(self)"; string item = "Blink";>;
float mAttenuationBulbP : CONTROLOBJECT<string name = "(self)"; string item = "AttenuationBulb+";>;
float mAttenuationBulbM : CONTROLOBJECT<string name = "(self)"; string item = "AttenuationBulb-";>;
float mMiePhaseP : CONTROLOBJECT<string name="(self)"; string item = "MiePhase+";>;
float mMiePhaseM : CONTROLOBJECT<string name="(self)"; string item = "MiePhase-";>;
float mMieDensityP : CONTROLOBJECT<string name="(self)"; string item = "MieDensity+";>;
float mMieDensityM : CONTROLOBJECT<string name="(self)"; string item = "MieDensity-";>;

float3 mPosition : CONTROLOBJECT<string name="(self)"; string item="Position";>;
float3 mDirection : CONTROLOBJECT<string name="(self)"; string item="Direction";>;

static const float FogAngle = radians(lerp(lerp(45.0f, 60.0f, mAngleP), 30.0f, mAngleM));
static const float FogBlink = saturate(1 - SmoothTriangleWave(time * mBlink * 10));
static const float FogMiePhase  = lerp(lerp(FogMieParams.x, FogMieParams.z, mMiePhaseP), FogMieParams.y, mMiePhaseM);
static const float FogRange = lerp(lerp(FogRangeParams.x, FogRangeParams.z, mRangeP), FogRangeParams.y, mRangeM);
static const float FogDensity = lerp(lerp(FogDensityParams.x, FogDensityParams.y, mMieDensityP), FogDensityParams.z, mMieDensityM);
static const float FogIntensity = lerp(lerp(FogIntensityParams.x, FogIntensityParams.z, mIntensityP), FogIntensityParams.y, mIntensityM);
static const float FogAttenuationBulb = lerp(lerp(FogAttenuationBulbParams.x, FogAttenuationBulbParams.z, mAttenuationBulbP), FogAttenuationBulbParams.y, mAttenuationBulbM);

static const float3 FogPosition = mPosition;
static const float3 FogDirection = normalize(mDirection - mPosition);
static const float3 FogColor = float3(mR, mG, mB) * FogIntensity * FogBlink;

static const float4x4 matLightProject = CreatePerspectiveProject(FogAngle, 1, mLightPlaneNear, mLightPlaneFar);
static const float4x4 matLightView = CreateLightViewMatrix(FogDirection, FogPosition);
static const float4x4 matLightViewProject = mul(matLightView, matLightProject);

#if VOLUMETRIC_FOG_ENABLE
#if VOLUMETRIC_FOG_QUALITY == 0
#	define VOLUMETRIC_FOG_SIZE 256
#elif VOLUMETRIC_FOG_QUALITY == 1
#	define VOLUMETRIC_FOG_SIZE 512
#elif VOLUMETRIC_FOG_QUALITY >= 2
#	define VOLUMETRIC_FOG_SIZE 1024
#elif VOLUMETRIC_FOG_QUALITY >= 3
#	define VOLUMETRIC_FOG_SIZE 2048
#else
#	define VOLUMETRIC_FOG_SIZE 256
#endif

texture VolumetricMap: OFFSCREENRENDERTARGET <
	string Description = "Volumetric fog Rendering for SpotLightIES";
	int Width = VOLUMETRIC_FOG_SIZE;
	int Height = VOLUMETRIC_FOG_SIZE;
	string Format = "R16F";
	float4 ClearColor = { 1, 1, 1, 1 };
	float ClearDepth = 1.0;
	string DefaultEffect = 
		"self = hide;"
		"*fog.pmx=hide;"
		"*controller.pmx=hide;"
		"*.pmx = ../ies_fog_shadow.fx;"
		"*.pmd = ../ies_fog_shadow.fx;"
		"* = hide";
>;
sampler VolumetricMapSamp = sampler_state {
	texture = <VolumetricMap>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

static float2 ShadowmapOffset  = 0.5 / VOLUMETRIC_FOG_SIZE;
static float2 ShadowmapOffset2 = 1.0 / VOLUMETRIC_FOG_SIZE;
#endif

texture IESTexture<string ResourceName = LIGHT_PARAMS_FILE; int Miplevels = 1;>; 
sampler IESTextureSamp = sampler_state
{
	texture = <IESTexture>;
	MinFilter = POINT; MagFilter = POINT; MipFilter = NONE;
	AddressU  = CLAMP; AddressV = CLAMP;
};

float ComputeIESLightProfile1D(float3 L, float3 Ld)
{
	return tex2Dlod(IESTextureSamp, float4(acos(dot(L, Ld)) / PI, 0, 0, 0)).r;
}

struct FogParams
{
	float3 FogPosition;
	float3 FogDirection;
	float3 FogColor;

	float FogAngle;
	float FogRange;
	float FogDensity;
	float FogIntensity;
	float FogMiePhase;
	float FogAttenuationBulb;

#if VOLUMETRIC_FOG_ENABLE
	sampler shadowMap;
	float2 shadowOffset;

	float4x4 matLightView;
	float4x4 matLightProject;
#endif
};

float ComputeRayPlaneIntersection(float3 position, float3 viewdir, float3 n, float dist)
{
	float a = dot(n, viewdir);
	if (a > 0.0) return -1;

	float t = -(dot(position, n) + dist) / a;
	return t;
}

float2 ComputeRayConeIntersection(float3 position, float3 viewdir, float3 center, float3 axis, float angle, float height)
{
	float3 origin = position - center;

	float A = dot(origin, viewdir);
	float B = dot(origin, axis);

	float cosTheta = dot(viewdir, axis);

	float a = cosTheta * cosTheta - angle * angle;
	float b = cosTheta * B - A * angle * angle;
	float c = B * B - dot(origin, origin) * angle * angle;

	float det = b * b - a * c;
	if (det < 0.0)
	{
		return -1;
	}
	else
	{
		det = sqrt(det);
		float t1 = (-b + det);
		float t2 = (-b - det);
		float2 minimaxIntersections = float2(t1, t2) / a;

		float3 cp = position + viewdir * minimaxIntersections.y - center;
		float h = dot(cp, axis);

		if (h <= 0.0 || h > height)
		{
			minimaxIntersections.y = ComputeRayPlaneIntersection(position, viewdir, -axis, height);
		}

		if (minimaxIntersections.x > minimaxIntersections.y)
		{
			minimaxIntersections.x = 0;
		}

		return minimaxIntersections;
	}
}

float ComputeOpticalDepth(FogParams param, float depth)
{
	return exp(-depth * param.FogDensity);
}

float GetPhysicalLightAttenuation(float3 L, float radius, float attenuationBulbSize)
{
	const float invRadius = 1 / radius;
	float d = length(L);
	float fadeoutFactor = saturate((radius - d) * (invRadius / 0.2h));
	d = max(d - attenuationBulbSize, 0);
	float denom = 1 + d / attenuationBulbSize;
	float attenuation = fadeoutFactor * fadeoutFactor / (denom * denom);
	return saturate(attenuation);
}

float GetSpotLightAttenuation(float3 L, float3 Ld, float cosAngle, float radius = 2)
{
	float falloff = cosAngle / (saturate(dot(L, Ld)) + 1e-6);
	float attenuation = 1.0 - pow(saturate(falloff), radius);
	return attenuation;
}

float HitShadowTest(FogParams param, sampler shadowMap, float2 shadowOffset, float4x4 matLightView, float4x4 matLightProject, float3 worldPosition)
{
	float4 proj = mul(float4(worldPosition, 1), matLightProject);
	proj.xy = PosToCoord(proj.xy / proj.w);
	proj.xy += shadowOffset;

	float linearDepth = length(mul(float4(worldPosition, 1), matLightView));
	return tex2Dlod(shadowMap, float4(proj.xy, 0, 0)).r < linearDepth ? 0 : 1;
}

float4 VolumetricFog(FogParams param, float3 V, float3 start, float3 end, float jitter, int stepNum = VOLUMETRIC_FOG_SAMPLES_LENGTH)
{
	float3 sampleStep = (end - start) / stepNum;
	float3 sampleStart = start + sampleStep * jitter;

	float sampleLength = length(sampleStep);
	float sampleOpticalDepth = ComputeOpticalDepth(param, sampleLength);

	float scatteringMie = 0;
	float scatteringDepth = 1;

	for (int i = 0; i < stepNum; i++, sampleStart += sampleStep, scatteringDepth *= sampleOpticalDepth)
	{
		float density = sampleLength;
		float3 L = sampleStart - param.FogPosition;
		float3 Lnormalized = normalize(L);

#if VOLUMETRIC_FOG_ENABLE
		density *= HitShadowTest(param, param.shadowMap, param.shadowOffset, param.matLightView, param.matLightProject, sampleStart);
		density *= ComputeIESLightProfile1D(Lnormalized, param.FogDirection);
#endif
		if (density)
		{
			float atten = GetPhysicalLightAttenuation(L, param.FogRange, param.FogAttenuationBulb);
			atten *= GetSpotLightAttenuation(Lnormalized, param.FogDirection, param.FogAngle);
			atten *= density;
			atten *= scatteringDepth;
#if VOLUMETRIC_FOG_ANISOTROPY == 2
			atten *= ComputePhaseMieHG(dot(-V, Lnormalized), param.FogMiePhase);
#endif
			scatteringMie += atten;
		}
	}

	float scattering = scatteringMie;
	scattering *=  param.FogIntensity;
#if VOLUMETRIC_FOG_ANISOTROPY == 1
	scattering *= ComputePhaseMieHG(dot(-V, param.FogDirection), param.FogMiePhase);
#endif

	return float4(param.FogColor * scattering + scattering * jitter / 255.0f, scattering * param.FogIntensity);
}

void VolumetricFogVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float3 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{
	Position.xyz = FogPosition + Normal * FogRange;
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
	oTexcoord1 = Position.xyz - CameraPosition;
}

float4 VolumetricFogPS(float4 texcoord : TEXCOORD0, float3 viewdir : TEXCOORD1, in float4 screenPosition : SV_Position) : COLOR
{
	FogParams param = (FogParams)0;
	param.FogPosition = FogPosition;
	param.FogDirection = FogDirection;
	param.FogAngle = cos(FogAngle);
	param.FogRange = FogRange;
	param.FogIntensity = FogIntensity;
	param.FogColor = FogColor;
	param.FogMiePhase = FogMiePhase;
	param.FogAttenuationBulb = FogAttenuationBulb;
	param.FogDensity = FogDensity;

#if VOLUMETRIC_FOG_ENABLE
	param.shadowMap = VolumetricMapSamp;
	param.shadowOffset = ShadowmapOffset;

	param.matLightView = matLightView;
	param.matLightProject = matLightViewProject;
#endif

	float3 V = normalize(viewdir);
	float2 intersections = ComputeRayConeIntersection(CameraPosition, V, param.FogPosition, param.FogDirection, param.FogAngle, param.FogRange);
	clip(intersections.y);

	float linearDepth = tex2Dlod(Gbuffer8Map, float4(texcoord.xy / texcoord.w, 0, 0)).r;
	clip(linearDepth - intersections.x);

	float3 start = CameraPosition + V * max(0, intersections.x);
	float3 end = CameraPosition + V * min(linearDepth, intersections.y);

	return VolumetricFog(param, V, start, end, PseudoRandom(screenPosition.xy * (time + PI)));
}

technique MainTechBS0<string MMDPass = "object_ss"
;>{
	pass DrawObject {
		ZEnable = FALSE; ZWriteEnable = FALSE;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 VolumetricFogVS();
		PixelShader  = compile ps_3_0 VolumetricFogPS();
	}
}

technique DepthTech<string MMDPass = "object";>{}
technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}