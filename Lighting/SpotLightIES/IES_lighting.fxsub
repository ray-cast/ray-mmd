#include "../../../ray.conf"
#include "../../../ray_advanced.conf"
#include "../../../shader/math.fxsub"
#include "../../../shader/common.fxsub"
#include "../../../shader/gbuffer.fxsub"
#include "../../../shader/gbuffer_sampler.fxsub"
#include "../../../shader/lighting.fxsub"
#include "../../../shader/arealight.fxsub"

#if SHADOW_MAP_FROM > 0
#	include "../../../shader/shadowcommon.fxsub"
#endif

float3 mPosition : CONTROLOBJECT<string name = "(self)"; string item = "Position";>;
float3 mDirection : CONTROLOBJECT<string name = "(self)"; string item = "Direction";>;

float mMultiLightP : CONTROLOBJECT<string name = "ray_controller.pmx"; string item = "MultiLight+";>;
float mMultiLightM : CONTROLOBJECT<string name = "ray_controller.pmx"; string item = "MultiLight-";>;

#if LIGHT_PARAMS_FROM == 0
float mR : CONTROLOBJECT<string name = "(self)"; string item = "R+";>;
float mG : CONTROLOBJECT<string name = "(self)"; string item = "G+";>;
float mB : CONTROLOBJECT<string name = "(self)"; string item = "B+";>;
float mBlink : CONTROLOBJECT<string name = "(self)"; string item = "Blink";>;
float mRangeP : CONTROLOBJECT<string name = "(self)"; string item = "Range+";>;
float mAngleM : CONTROLOBJECT<string name = "(self)"; string item = "Angle-";>;
float mHardnessP : CONTROLOBJECT<string name = "(self)"; string item = "Hardness";>;
float mIntensityP : CONTROLOBJECT<string name = "(self)"; string item = "Intensity+";>;
float mIntensityM : CONTROLOBJECT<string name = "(self)"; string item = "Intensity-";>;
float mAttenuationBulbP : CONTROLOBJECT<string name = "(self)"; string item = "AttenuationBulb+";>;

static float LightFalloff = 2;
static float LightRange = lerp(lightRangeLimits.x, lightRangeLimits.y, mRangeP);
static float LightAngle = radians(lerp(60.0f, 30.0f, mAngleM));
static float LightHardness = lerp(0.15, 0.5, mHardnessP);
static float LightAttenuationBulb = lerp(1, mLightAttenuationBulbMax, mAttenuationBulbP);
static float LightIntensity = lerp(lerp(lightIntensityLimits.x, lightIntensityLimits.y, mIntensityP), 0, mIntensityM);
static float LightIntensity2 = lerp(lerp(mLightIntensityMin, mLightIntensityMax, mMultiLightP), 0, mMultiLightM);

static float3 LightColor = float3(mR, mG, mB) * LightIntensity * saturate(1 - SmoothTriangleWave(time * mBlink * 10));
#else
static const float LightRange = lightRange;
static const float LightAngle = radians(lightSpotAngle);
static const float LightFalloff = lightSpotFalloff;
static const float LightHardness = shadowHardness;
static const float LightIntensity2 = lerp(lerp(mLightIntensityMin, mLightIntensityMax, mMultiLightP), 0, mMultiLightM);
static const float LightAttenuationBulb = lightAttenuationBulb;

static const float3 LightColor = lightColor * saturate(1 - SmoothTriangleWave(time * lightBlink));
#endif

static float3 LightPosition = mPosition;
static float3 LightDirection = normalize(mDirection - mPosition);

static float3 viewLightPosition = mul(float4(mDirection, 1), matView).xyz;
static float3 viewLightDirection = normalize(mul(-LightDirection, (float3x3)matView));

#if SHADOW_MAP_FROM > 0
#if SHADOW_MAP_QUALITY == 0
#   define SHADOW_MAP_SIZE 256
#elif SHADOW_MAP_QUALITY == 1
#   define SHADOW_MAP_SIZE 512
#elif SHADOW_MAP_QUALITY >= 2
#   define SHADOW_MAP_SIZE 1024
#elif SHADOW_MAP_QUALITY >= 3
#   define SHADOW_MAP_SIZE 2048
#else
#   define SHADOW_MAP_SIZE 256
#endif

texture ShadowMap: OFFSCREENRENDERTARGET<
	string Description = "Shadow Rendering for SpotLightIES";
	string Format = "G32R32F";
	int2 Dimensions = {SHADOW_MAP_SIZE, SHADOW_MAP_SIZE};
	float4 ClearColor = { 0, 0, 0, 0 };
	float ClearDepth = 1.0;
	string DefaultEffect = 
		"self = hide;"
		"*fog.pmx=hide;"
		"*controller*.pmx=hide;"
		"*editor*.pmx=hide;"
		"*.pmx = ../ies_shadow.fx;"
		"*.pmd = ../ies_shadow.fx;"
		"* = hide";
>;

sampler ShadowMapSamp = sampler_state {
	texture = <ShadowMap>;
	MinFilter = POINT; MagFilter = LINEAR; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

static float2 ShadowmapOffset  = 0.5 / SHADOW_MAP_SIZE;
static float2 ShadowmapOffset2 = 1.0 / SHADOW_MAP_SIZE;

static float4x4 matLightProject = CreatePerspectiveProject(LightAngle, 1, mLightPlaneNear, mLightPlaneFar);
static float4x4 matLightView = CreateLightViewMatrix(LightDirection, LightPosition);
static float4x4 matLightViewProject = mul(matLightView, matLightProject);
static float4x4 matLightViewFromCameraView = mul(matViewInverse, matLightView);
static float4x4 matLightProjectFromCameraView = mul(matViewInverse, matLightViewProject);

float ShadingShadow(MaterialParam material, float3 P, float3 V)
{
	float4 proj = mul(float4(P, 1), matLightProjectFromCameraView);
	proj.xy = PosToCoord(proj.xy / proj.w);
	proj.xy += ShadowmapOffset;

	float2 shadow = 0.0;

	[unroll]
	for (int x = -sampleRadius; x <= sampleRadius; x++) 
	{
		[unroll]
		for (int y = -sampleRadius; y <= sampleRadius; y++)
		{
			float2 pos = proj.xy + float2(x, y) * ShadowmapOffset2;
			shadow += tex2Dlod(ShadowMapSamp, float4(pos, 0, 0)).rg * sampleKernel[sampleRadius + x] * sampleKernel[sampleRadius + y];
		}
	}

	float linearDepth = length(mul(float4(P, 1), matLightViewFromCameraView));
	return ChebyshevUpperBound(shadow, linearDepth, 1e-5, LightHardness);
}
#endif

texture IESTexture<string ResourceName = LIGHT_PARAMS_FILE; int Miplevels = 1;>; 
sampler IESTextureSamp = sampler_state
{
	texture = <IESTexture>;
	MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE;
	AddressU  = CLAMP; AddressV = CLAMP;
};

float3 ComputeIESLightProfile1D(float3 L, float3 Ld)
{
	L = mul(L, (float3x3)matViewInverse);
	float angle = (1 - acos(dot(L, Ld)) / PI);
	return tex2Dlod(IESTextureSamp, float4(angle, 0, 0, 0)).rgb;
}

void ShadingMaterial(MaterialParam material, float3 P, float3 V, out float3 diffuse, out float3 specular)
{
	float3 L = viewLightPosition - P;
	float3 L2 = normalize(L);

	float3 atten = GetPhysicalLightAttenuation(L, LightRange, LightAttenuationBulb);
	atten *= GetSpotLightAttenuation(L2, viewLightDirection, LightAngle, LightFalloff);
	atten *= ComputeIESLightProfile1D(L2, LightDirection);

	float4 energy = SurfaceEnergy(material, material.normal, L2, V, 1.0);
	diffuse = energy.r + SubsurfaceShadingFast(material, material.normal, L2, V);
	diffuse *= atten * LightColor * LightIntensity2;

#if LIGHT_PARAMS_TYPE == 0
	specular = energy.gba;
	specular *= atten * LightColor * LightIntensity2;
#endif
}

void GetGbufferParams(float4 texcoord, out MaterialParam material, out MaterialParam materialAlpha)
{
	float2 coord = texcoord.xy / texcoord.w;

	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);

	clip(sum(materialAlpha.albedo + materialAlpha.specular) - 1e-5);

	float4 MRT1 = tex2Dlod(Gbuffer1Map, float4(coord, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer2Map, float4(coord, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer3Map, float4(coord, 0, 0));
	float4 MRT4 = tex2Dlod(Gbuffer4Map, float4(coord, 0, 0));

	DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);
}

void LightSourceVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{    
	Position.xyz = LightPosition + Normal * LightRange;
	oTexcoord1 = -mul(Position, matView);
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
}

void LightSourcePS(
	float4 texcoord : TEXCOORD0, 
	float3 viewdir : TEXCOORD1,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	MaterialParam material;
	MaterialParam materialAlpha;
	GetGbufferParams(texcoord, material, materialAlpha);

	float3 V = normalize(viewdir);

	float3 P1 = V * material.linearDepth / V.z;
	float3 P2 = V * materialAlpha.linearDepth / V.z;

	float3 diffuse, specular = 0;
	ShadingMaterial(material, P1, V, diffuse, specular);

	float3 diffuseAlpha, specularAlpha = 0;
	ShadingMaterial(materialAlpha, P2, V, diffuseAlpha, specularAlpha);

#if SSSS_QUALITY
	oColor0 = float4(diffuse, 0);
#else
	oColor0 = float4(diffuse * material.albedo, 0);
#endif
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);

#if SHADOW_MAP_FROM > 0
	clip(sum(oColor0 + oColor1 + oColor2) - 1e-5);

	float shadow = ShadingShadow(materialAlpha, P2, V);
	oColor0 *= shadow;
	oColor1 *= shadow;
	oColor2 *= shadow;
#endif
}

void LightSourceShadowlessPS(
	float4 texcoord : TEXCOORD0, 
	float3 viewdir : TEXCOORD1,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	MaterialParam material;
	MaterialParam materialAlpha;
	GetGbufferParams(texcoord, material, materialAlpha);

	float3 V = normalize(viewdir);

	float3 P1 = V * material.linearDepth / V.z;
	float3 P2 = V * materialAlpha.linearDepth / V.z;

	float3 diffuse, specular = 0;
	ShadingMaterial(material, P1, V, diffuse, specular);

	float3 diffuseAlpha, specularAlpha = 0;
	ShadingMaterial(materialAlpha, P2, V, diffuseAlpha, specularAlpha);

#if SSSS_QUALITY
	oColor0 = float4(diffuse, 0);
#else
	oColor0 = float4(diffuse * material.albedo, 0);
#endif
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);
}

technique MainTech0<string MMDPass = "object";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourceShadowlessPS();
	}
}

technique MainTecBS0<string MMDPass = "object_ss";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourcePS();
	}
}

technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}