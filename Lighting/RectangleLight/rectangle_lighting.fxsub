#include "../../ray.conf"
#include "../../shader/math.fxsub"
#include "../../shader/common.fxsub"
#include "../../shader/gbuffer.fxsub"
#include "../../shader/gbuffer_sampler.fxsub"
#include "../../shader/lighting.fxsub"
#include "../../shader/shadowcommon.fxsub"

#define RECTANGLELIGHT_MAX_WIDTH 50
#define RECTANGLELIGHT_MAX_HEIGHT 50

float mR : CONTROLOBJECT < string name="(self)"; string item = "R+"; >;
float mG : CONTROLOBJECT < string name="(self)"; string item = "G+"; >;
float mB : CONTROLOBJECT < string name="(self)"; string item = "B+"; >;
float mRangeP : CONTROLOBJECT < string name="(self)"; string item = "Range+"; >;
float mIntensityP : CONTROLOBJECT < string name="(self)"; string item = "Intensity+"; >;
float mIntensityM : CONTROLOBJECT < string name="(self)"; string item = "Intensity-"; >;
float mBlink : CONTROLOBJECT < string name="(self)"; string item = "Blink"; >;
float mWidthP : CONTROLOBJECT < string name="(self)"; string item = "Width+"; >;
float mHeightP : CONTROLOBJECT < string name="(self)"; string item = "Height+"; >;
float3 mPosition : CONTROLOBJECT < string name="(self)"; string item = "Position"; >;
float3 mDirection : CONTROLOBJECT < string name="(self)"; string item = "Direction"; >;
float3 mRightTop : CONTROLOBJECT < string name="(self)"; string item = "RightTop"; >;
float3 mRightBottom : CONTROLOBJECT < string name="(self)"; string item = "RightBottom"; >;

static float LightIntensity = (1 - mIntensityM) * LIGHTSOURCE_MIN_INTENSITY + mIntensityP * LIGHTSOURCE_MAX_INTENSITY;
static float LightRange = (1 + mRangeP * LIGHTSOURCE_MAX_RANGE);
static float LightWidth = (1 + mWidthP * RECTANGLELIGHT_MAX_WIDTH);
static float LightHeight = (1 + mHeightP * RECTANGLELIGHT_MAX_HEIGHT);

static float3 LightPosition = mPosition;
static float3 LightDirection = normalize(mDirection - mPosition);

static float3 viewLightPosition = mul(float4(LightPosition, 1), matView).xyz;
static float3 viewLightDirection = normalize(mul(LightDirection, (float3x3)matView));
static float3 viewLightRight = normalize(mul((mRightTop + mRightBottom) / 2 - LightPosition, (float3x3)matView));
static float3 viewLightUp = normalize(cross(viewLightDirection, viewLightRight));

static float2 LightRect = float2(LightWidth, LightHeight);

#if VIDEO_MAP_ENABLE && VIDEO_MAP_IN_TEXTURE != 1
    #if VIDEO_MAP_IN_SCREEN_MAP
        shared texture2D DummyScreenTex : RenderColorTarget;
    #elif VIDEO_MAP_ANIMATION_ENABLE
        texture VideoMap : ANIMATEDTEXTURE <string ResourceName = VIDEO_MAP_FILE; float speed=VIDEO_MAP_ANIMATION_SPEED;>;
    #else
        texture VideoMap<string ResourceName = VIDEO_MAP_FILE;>;
    #endif
    sampler VideoMapSamp = sampler_state
    {
#if VIDEO_MAP_IN_SCREEN_MAP
        texture = DummyScreenTex;
#else
        texture = VideoMap;
#endif
        MINFILTER = LINEAR; MAGFILTER = LINEAR; MipFilter = LINEAR; ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#elif VIDEO_MAP_ENABLE
    texture VideoMap: MATERIALTEXTURE;
    sampler VideoMapSamp = sampler_state
    {
        texture = <VideoMap>;
        MINFILTER = LINEAR; MAGFILTER = LINEAR; MipFilter = LINEAR; ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

float4 GaussianBlur(sampler source, float2 coord, float2 offset, int radius, float sigma, float mip)
{
    float totalWeight = 0.0;
    float4 totalColor = 0.0;
    
    [unroll]
    for (int i = -radius; i < radius; ++i)
    {
        float gaussianX = exp(-(i * i) / (2.0 * sigma * sigma));
        float offsetX = i * offset.x;
     
        [unroll]
        for(int j = -radius; j < radius; ++j)
        {
            float gaussianY = exp(-(j * j) / (2.0 * sigma * sigma));

            float offsetY = j * offset.y;
            float4 color = tex2Dlod(source, float4((coord + float2(offsetX, offsetY)) * vedioMapLoopNum, 0, mip));

            totalWeight += gaussianX * gaussianY;
            totalColor += color * gaussianX * gaussianY;
        }
    }

    return (totalColor / totalWeight);
}

float4 ShadingMaterial(MaterialParam material, float3 V)
{
    float3 P = V * material.linearDepth / V.z;
    
    float2 coord1, coord2;
    float3 L = viewLightPosition - P;
    float3 Lw = RectangleDirection(L, viewLightRight, viewLightUp, viewLightDirection, LightRect, coord1);
    float3 L2 = normalize(Lw);
    
    float atten1 = GetPhysicalLightAttenuation(Lw, LightRange);
    
    #if RECTANGLELIGHT_TWOSIDE_LIGHTING == 0
        atten1 *= RectangleAttenuation(Lw, viewLightDirection, PI / 2, LightRange);
    #endif
    
    float atten2 = saturate(dot(material.normal, L2)) * atten1;
    
    float3 diffuse = material.albedo * DiffuseBRDF(material.normal, L2, V, material.smoothness, material.transmittance);
    
    #if VIDEO_MAP_ENABLE
        float mip = (1 - material.smoothness * material.smoothness) * 6;
        diffuse *= srgb2linear(GaussianBlur(VideoMapSamp, coord1, ViewportOffset2, 3, 10, mip).rgb);
    #endif
    
    float3 specular = RectangleLightBRDF(material.normal, V, L, viewLightRight, viewLightUp, viewLightDirection, LightRect, material.smoothness, material.specular, coord2);
       
    #if VIDEO_MAP_ENABLE
        if (coord2.x >= 1.0 || coord2.y >= 1.0 ||
            coord2.x <= 0.0 || coord2.y <= 0.0)
        {
            specular *= 0.0;
        }
        else
        {
            float mip = (1 - material.smoothness * material.smoothness) * 6;
            specular *= srgb2linear(tex2Dlod(VideoMapSamp, float4(coord2 * vedioMapLoopNum, 0, mip)).rgb);
        }
    #endif
    
    float4 lighting = 0;
    lighting.rgb += atten1 * diffuse;
    lighting.rgb += atten2 * specular;    
    lighting.rgb *= float3(mR, mG, mB) * LightIntensity * step(0, material.albedo + material.specular - 1e-5);
    lighting *= saturate(1 - SmoothTriangleWave(time * (mBlink * 10)));
    
    return lighting;
}

void LightSourceVS(
    in float4 Position : POSITION,
    in float3 Normal : NORMAL,
    in float2 Texcoord : TEXCOORD0,
    out float4 oTexcoord0 : TEXCOORD0,
    out float4 oTexcoord1 : TEXCOORD1,
    out float4 oPosition  : POSITION)
{
    Position.xyz = LightPosition + Normal * LightRange;
    
    oPosition = float4(CoordToPos(Texcoord), 0, 1);
    oTexcoord0 = Texcoord.xyxy + ViewportOffset.xyxy;
    oTexcoord1 = -mul(oPosition, matProjectInverse);
}

void LightSourcePS(
    float2 coord : TEXCOORD0, 
    float3 viewdir : TEXCOORD1,
    out float4 oColor0 : COLOR0,
    out float4 oColor1 : COLOR1)
{
    float4 MRT1 = tex2D(Gbuffer1Map, coord);
    float4 MRT2 = tex2D(Gbuffer2Map, coord);
    float4 MRT3 = tex2D(Gbuffer3Map, coord);
    float4 MRT4 = tex2D(Gbuffer4Map, coord);

    MaterialParam material;
    DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);
    
    float4 MRT5 = tex2D(Gbuffer5Map, coord);
    float4 MRT6 = tex2D(Gbuffer6Map, coord);
    float4 MRT7 = tex2D(Gbuffer7Map, coord);
    float4 MRT8 = tex2D(Gbuffer8Map, coord);

    MaterialParam materialAlpha;
    DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);
    
    float3 V = normalize(viewdir);
    
    oColor0 = ShadingMaterial(material, V);
    oColor1 = ShadingMaterial(materialAlpha, V);
}

#define OBJECT_TEC(name, mmdpass) \
    technique name < string MMDPass = mmdpass;\
        string Script = \
            "RenderColorTarget0=;"\
            "RenderColorTarget1=LightAlphaMap;"\
            "Pass=DrawObject;"\
    ;> { \
        pass DrawObject { \
            ZEnable = false; ZWriteEnable = false;\
            AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;\
            SrcBlend = ONE; DestBlend = ONE;\
            VertexShader = compile vs_3_0 LightSourceVS(); \
            PixelShader  = compile ps_3_0 LightSourcePS(); \
        } \
    }

OBJECT_TEC(MainTec0, "object")
OBJECT_TEC(MainTecBS0, "object_ss")

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTech < string MMDPass = "shadow";  > {}
technique ZplotTec < string MMDPass = "zplot"; > {}