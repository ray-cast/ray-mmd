#include "../../ray.conf"
#include "../../ray_advanced.conf"
#include "../../shader/math.fxsub"
#include "../../shader/common.fxsub"
#include "../../shader/gbuffer.fxsub"
#include "../../shader/gbuffer_sampler.fxsub"

float mFogH :  CONTROLOBJECT<string name="(self)"; string item = "H+";>;
float mFogS :  CONTROLOBJECT<string name="(self)"; string item = "S+";>;
float mFogV :  CONTROLOBJECT<string name="(self)"; string item = "V-";>;
float mFogRangeP : CONTROLOBJECT<string name="(self)"; string item = "Range+";>;
float mFogRangeM : CONTROLOBJECT<string name="(self)"; string item = "Range-";>;
float mFogDensityP : CONTROLOBJECT<string name="(self)"; string item = "Density+";>;
float mFogIntensityP : CONTROLOBJECT<string name="(self)"; string item = "Intensity+";>;

static float FogRange = lerp(lerp(1.0, 1.0 / FOG_MAX_RANGE, mFogRangeP), FOG_MIN_RANGE, mFogRangeM);
static float FogDensity = lerp(FOG_MIN_DENSITY, 1, mFogDensityP);
static float FogIntensity = lerp(FOG_MIN_INTENSITY, FOG_MAX_INTENSITY, mFogIntensityP);

static float3 FogColor = hsv2rgb(float3(mFogH, mFogS, 1 - mFogV));

void GlobalFogVS(
	in float4 Position : POSITION,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oPosition : POSITION)
{
	oPosition = float4(CoordToPos(Texcoord), 0, 1);
	oTexcoord0 = Texcoord.xyxy + ViewportOffset.xyxy;
}

float4 GlobalFogPS(float2 coord : TEXCOORD0) : COLOR
{
	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	MaterialParam material;
	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, material);

	float fogAmount = 1 - exp(-material.linearDepth / FogDensity);
	fogAmount = pow(fogAmount, FogRange);

#if FOG_DISCARD_SKY
	fogAmount *=  step(0, sum(material.albedo + material.specular) - 1e-5);
#endif

	return float4(FogColor * fogAmount * FogIntensity, 0);
}

#define OBJECT_TEC(name, mmdpass) \
	technique name<string MMDPass = mmdpass;>{\
		pass DrawObject {\
			ZEnable = false; ZWriteEnable = false;\
			AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;\
			SrcBlend = ONE; DestBlend = ONE;\
			CullMode = CCW;\
			VertexShader = compile vs_3_0 GlobalFogVS();\
			PixelShader  = compile ps_3_0 GlobalFogPS();\
		}\
	}

OBJECT_TEC(MainTec0, "object")
OBJECT_TEC(MainTecBS0, "object_ss")

technique EdgeTec<string MMDPass = "edge";> {}
technique ShadowTech<string MMDPass = "shadow";> {}
technique ZplotTec<string MMDPass = "zplot";> {}