#include "../../ray.conf"
#include "../../ray_advanced.conf"
#include "../../shader/math.fxsub"
#include "../../shader/common.fxsub"
#include "../../shader/Color.fxsub"
#include "../../shader/Packing.fxsub"
#include "../../shader/shadowcommon.fxsub"
#include "../../shader/gbuffer.fxsub"
#include "../../shader/gbuffer_sampler.fxsub"
#include "../../shader/DeclareGbufferTexture.fxsub"
#include "../../shader/PhaseFunctions.fxsub"

float mR : CONTROLOBJECT<string name="(self)"; string item="R+";>;
float mG : CONTROLOBJECT<string name="(self)"; string item="G+";>;
float mB : CONTROLOBJECT<string name="(self)"; string item="B+";>;
float mRangeP : CONTROLOBJECT<string name="(self)"; string item="Range+";>;
float mRangeM : CONTROLOBJECT<string name="(self)"; string item="Range-";>;
float mIntensityP : CONTROLOBJECT<string name="(self)"; string item="Intensity+";>;
float mIntensityM : CONTROLOBJECT<string name="(self)"; string item="Intensity-";>;
float mMiePhaseP : CONTROLOBJECT<string name="(self)"; string item="MiePhase+";>;
float mMiePhaseM : CONTROLOBJECT<string name="(self)"; string item="MiePhase-";>;
float mMieDensityP : CONTROLOBJECT<string name="(self)"; string item="Density+";>;
float mMieDensityM : CONTROLOBJECT<string name="(self)"; string item="Density-";>;
float mWidthP : CONTROLOBJECT<string name="(self)"; string item="Width+";>;
float mHeightP : CONTROLOBJECT<string name="(self)"; string item="Height+";>;
float mDepthP : CONTROLOBJECT<string name="(self)"; string item="Depth+";>;

float3 mPosition : CONTROLOBJECT<string name="(self)"; string item = "Position";>;

static const float FogWidth = lerp(FogRangeParams.x, FogRangeParams.z, mWidthP);
static const float FogHeight = lerp(FogRangeParams.x, FogRangeParams.z, mHeightP);
static const float FogDepth = lerp(FogRangeParams.x, FogRangeParams.z, mDepthP);
static const float FogMiePhase  = lerp(lerp(FogMieParams.x, FogMieParams.z, mMiePhaseP), FogMieParams.y, mMiePhaseM);
static const float FogDensity = lerp(lerp(FogDensityParams.x, FogDensityParams.z, mMieDensityP), FogDensityParams.y, mMieDensityM);
static const float FogIntensity = lerp(lerp(FogIntensityParams.x, FogIntensityParams.z, mIntensityP), FogIntensityParams.y, mIntensityM);

static const float3 FogPosition = mPosition;
static const float3 FogColor = float3(mR, mG, mB) * FogIntensity;

struct FogParams
{
	float3 FogColor;
	float3 FogPosition;
	float3 FogRange;

	float FogDensity;
	float FogIntensity;
	float FogMiePhase;
};

void ComputeCubeMinMax(float3 d, float3 q, float3 size, out float3 tmin, out float3 tmax)
{
	const float3 aabb[2] = { q - size, q + size };

	float3 symbol = step(0, d);

	tmin.x = aabb[1-symbol[0]].x;
	tmin.y = aabb[1-symbol[1]].y;
	tmin.z = aabb[1-symbol[2]].z;

	tmax.x = aabb[symbol[0]].x;
	tmax.y = aabb[symbol[1]].y;
	tmax.z = aabb[symbol[2]].z;
}

float2 ComputeRayCubeIntersection(float3 p, float3 d, float3 q, float3 size, float3 tmin, float3 tmax)
{
	const float3 inv = 1.0 / (d + 1e-16);

	tmin = (tmin - p) * inv;
	tmax = (tmax - p) * inv;

	tmin.x = max(max(tmin.x, tmin.y), tmin.z);
	tmax.x = min(min(tmax.x, tmax.y), tmax.z);

	if (tmin.x > tmax.x)
		return -1;
	else
		return float2(tmin.x, tmax.x);
}

float2 ComputeRayCubeIntersection(float3 p, float3 d, float3 q, float3 size)
{
	float3 tmin, tmax;
	ComputeCubeMinMax(d, q, size, tmin, tmax);
	return ComputeRayCubeIntersection(p, d, q, size, tmin, tmax);
}

float ComputeOpticalDepth(FogParams param, float depth)
{
	return exp(-depth * param.FogDensity);
}

float4 VolumetricFog(FogParams param, float3 V, float3 L, float3 start, float3 end, float jitter, int stepNum = VOLUMETRIC_FOG_SAMPLES_LENGTH)
{
	float3 sampleStep = (end - start) / stepNum;
#if VOLUMETRIC_FOG_SAMPLES_WITH_JITTER
	float3 sampleStart = start + sampleStep * jitter;
#else
	float3 sampleStart = start + sampleStep;
#endif

	float sampleLength = length(sampleStep);
	float sampleOpticalDepth = ComputeOpticalDepth(param, sampleLength);

	float scatteringMie = 0;
	float scatteringDepth = 1;

	float3 tmin, tmax;
	ComputeCubeMinMax(L, param.FogPosition, param.FogRange, tmin, tmax);

	for (int i = 0; i < stepNum; i++, sampleStart += sampleStep, scatteringDepth *= sampleOpticalDepth)
	{
		float2 intersections = ComputeRayCubeIntersection(sampleStart, L, param.FogPosition, param.FogRange, tmin, tmax);
		float3 sampleLightPoint = sampleStart + L * intersections.y;

		float scattering = ComputeOpticalDepth(param, distance(sampleLightPoint, sampleStart));
		scattering *= sampleOpticalDepth;
		scattering *= scatteringDepth;

		scatteringMie += scattering;
	}

	float3 scattering = param.FogColor * scatteringMie * sampleLength;
	scattering *= param.FogIntensity;
#if VOLUMETRIC_FOG_ANISOTROPY
	scattering *= ComputePhaseMieHG(dot(V, L), param.FogMiePhase);
#endif

	return float4(scattering, -sampleLength * param.FogIntensity * stepNum * param.FogDensity);
}

void VolumetricCubeVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	out float4 oTexcoord0 : TEXCOORD0,
	out float3 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{
	Position.xyz = (Position.xyz - FogPosition) * float3(FogWidth, FogHeight, FogDepth) + FogPosition;
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
	oTexcoord1 = Position.xyz - CameraPosition;
}

float4 VolumetricCubePS(float4 texcoord : TEXCOORD0, float4 viewdir : TEXCOORD1) : COLOR
{
	FogParams param;
	param.FogPosition = FogPosition;
	param.FogRange = float3(FogWidth, FogHeight, FogDepth);
	param.FogIntensity = FogIntensity;
	param.FogColor = FogColor;
	param.FogMiePhase = FogMiePhase;
	param.FogDensity = FogDensity * (FogRangeParams.z * FogRangeParams.z * 3) / dot(param.FogRange, param.FogRange);

	texcoord.xy /= texcoord.w;

	float3 V = normalize(viewdir);

	float2 intersections = ComputeRayCubeIntersection(CameraPosition, V, param.FogPosition, param.FogRange);
	clip(intersections.y);

	float linearDepth = SampleLinearEyeDepth(texcoord.xy / texcoord.w);
	clip(linearDepth - intersections.x);

	float3 start = CameraPosition + V * max(0, intersections.x);
	float3 end = CameraPosition + V * min(linearDepth, intersections.y);

	return VolumetricFog(param, V, normalize(-MainLightDirection), start, end, GetJitterOffset(texcoord.xy * ViewportSize + time));
}

technique MainTechBS0<string MMDPass = "object_ss"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = true; AlphaTestEnable = false;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 VolumetricCubeVS();
		PixelShader = compile ps_3_0 VolumetricCubePS();
	}
}

technique DepthTech<string MMDPass = "object";>{}
technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}