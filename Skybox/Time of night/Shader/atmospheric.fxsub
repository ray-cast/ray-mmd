struct ScatteringParams
{
	float sunSize;
	float sunRadiance;

	float mieG;
	float mieHeight;

	float rayleighHeight;

	float3 waveLambda;
	float3 waveLambdaMie;
	float3 waveLambdaRayleigh;
	
	float fogRange;
};

float ComputeOpticalMass(float cosAngle)
{
	float cosAngleRadians = degrees(acos(cosAngle));
	return 1.0 / (cosAngle + 0.15 * pow(abs(93.885 - cosAngleRadians), -1.253));
}

float3 ComputeScatteringFunctions(float3 inscatteringMie, float3 inscatteringRayleigh, float3 V, float3 L, float g)
{
	float sunTheta = dot(V, L);

	float3 betaMie1 = inscatteringMie * ComputePhaseMie(sunTheta, g);
	float3 betaRayleigh = inscatteringRayleigh * ComputePhaseRayleigh(sunTheta);

	float3 betaTotal = (betaMie1 + betaRayleigh);
	return betaTotal / (inscatteringMie + inscatteringRayleigh);
}

float ComputeSunIntensity(float EE, float zenithAngleCos)
{
	float steepness = 1.5;
	float cutoffAngle = PI / 1.95;
	return EE * saturate(1.0f - exp(-((cutoffAngle - acos(zenithAngleCos)) / steepness)));
}

float4 ComputeSkyScattering(ScatteringParams setting, float3 V, float3 L)
{
	float3 up = float3(0, 1, 0);
	float3 sunDirection = normalize(-L);

	float zenithAngle = saturate(dot(V, up));
	float zenithAngleCos = dot(sunDirection, up);
	float zenithAngleMass = ComputeOpticalMass(zenithAngle);

	float3 inscatteringMie = setting.waveLambdaMie;
	float3 inscatteringRayleigh = setting.waveLambdaRayleigh;

	float3 inscattering = ComputeScatteringFunctions(inscatteringMie, inscatteringRayleigh, V, sunDirection, setting.mieG);
	inscattering *= ComputeSunIntensity(setting.sunRadiance, zenithAngleCos);

	float3 opticalMie = inscatteringMie * setting.mieHeight * zenithAngleMass;
	float3 opticalRayleigh = inscatteringRayleigh * setting.rayleighHeight * zenithAngleMass;
	float3 extinction = exp(-(opticalMie + opticalRayleigh));

	float3 Lin = inscattering * (1.0 - extinction);

	return float4(Lin, 1);
}

float3 ComputeSkyFog(ScatteringParams setting, float distance, float3 V, float3 L)
{
	float3 sunUp = float3(0, 1, 0);
	float3 sunDirection = normalize(-L);

	float zenithAngle = saturate(dot(V, sunUp));
	float zenithAngleCos = dot(sunDirection, sunUp);
	float zenithAngleMass = ComputeOpticalMass(zenithAngle);

	float3 inscatteringMie = setting.waveLambdaMie;
	float3 inscatteringRayleigh = setting.waveLambdaRayleigh;

	float cosTheta = dot(V, sunDirection);

	float3 betaMie = inscatteringMie * ComputePhaseMie(cosTheta, setting.mieG) * (4 * PI);
	float3 betaRayleigh = inscatteringRayleigh * ComputePhaseRayleigh(cosTheta) * (4 * PI);
	float3 betaTotal = (betaMie + betaRayleigh);

	float3 inscattering = betaTotal / (inscatteringMie + inscatteringRayleigh);
	inscattering *= ComputeSunIntensity(setting.sunRadiance, zenithAngleCos);

	float3 extinction = exp(-(inscatteringMie + inscatteringRayleigh) * distance * setting.fogRange);

	float3 Lin = inscattering * (1.0 - extinction);
	return Lin;
}