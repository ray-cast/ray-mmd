float4x4 matView : VIEW;
float4x4 matWorld : WORLD;
float4x4 matWorldView : WORLDVIEW;
float4x4 matWorldViewProject : WORLDVIEWPROJECTION;

float4 MaterialDiffuse : DIFFUSE  < string Object = "Geometry"; >;
float3 MaterialSpecular : SPECULAR < string Object = "Geometry"; >;
float  MaterialPower : SPECULARPOWER < string Object = "Geometry"; >;

float4 TextureAddValue : ADDINGTEXTURE;
float4 TextureMulValue : MULTIPLYINGTEXTURE;

#if EMISSIVE_APPLY_MORPH_COLOR || ALBEDO_APPLY_MORPH_COLOR
float MorphRed   : CONTROLOBJECT < string name="(self)"; string item = "R+"; >;
float MorphGreen : CONTROLOBJECT < string name="(self)"; string item = "G+"; >;
float MorphBlue  : CONTROLOBJECT < string name="(self)"; string item = "B+"; >;
#endif

#if EMISSIVE_APPLY_MORPH_INTENSITY
float mIntensityP : CONTROLOBJECT < string name="(self)"; string item = "Intensity+"; >;
float mIntensityM : CONTROLOBJECT < string name="(self)"; string item = "Intensity-"; >;

float emissiveIntensityMin = log(50);
float emissiveIntensityMax = log(2100);
static float LightIntensity = lerp(emissiveIntensityMin, emissiveIntensityMax, mIntensityP - mIntensityM);
#endif

#define TEXTURE_USE_LINEAR 0
#define TEXTURE_USE_NEAREST 0
#define TEXTURE_USE_ANISOTROPY 1
#define TEXTURE_USE_MIP_NONE 0
#define TEXTURE_USE_MIP_LINEAR 0
#define TEXTURE_USE_MIP_NEAREST 0
#define TEXTURE_USE_MIP_ANISOTROPIC 1
#define TEXTURE_ANISOTROPY_LEVEL 16

#if TEXTURE_USE_LINEAR
#   define TEXTURE_FILTER LINEAR
#elif TEXTURE_USE_NEAREST
#   define TEXTURE_FILTER POINT
#elif TEXTURE_USE_ANISOTROPY
#   define TEXTURE_FILTER ANISOTROPIC
#else
#   define TEXTURE_FILTER POINT
#endif

#if TEXTURE_USE_MIP_LINEAR
#   define TEXTURE_MIP_FILTER LINEAR
#elif TEXTURE_USE_MIP_NEAREST
#   define TEXTURE_MIP_FILTER POINT
#elif TEXTURE_USE_MIP_ANISOTROPIC
#   define TEXTURE_MIP_FILTER ANISOTROPIC
#elif TEXTURE_USE_MIP_NONE
#   define TEXTURE_MIP_FILTER NONE
#else
#   define TEXTURE_MIP_FILTER POINT
#endif

#define PI 3.141592654f

#define LIGHTINGMODEL_NORMAL         0
#define LIGHTINGMODEL_TRANSMITTANCE  1
#define LIGHTINGMODEL_EMISSIVE       2

#define MIDPOINT_8_BIT (127.0f / 255.0f)
#define MAX_FRACTIONAL_8_BIT (255.0f / 256.0f)
#define TWO_BITS_EXTRACTION_FACTOR (3.0f + MAX_FRACTIONAL_8_BIT)

#define ALPHA_THRESHOLD 0.98

shared texture Gbuffer2RT: RENDERCOLORTARGET;
shared texture Gbuffer3RT: RENDERCOLORTARGET;
shared texture Gbuffer4RT: RENDERCOLORTARGET;
shared texture Gbuffer5RT: RENDERCOLORTARGET;
shared texture Gbuffer6RT: RENDERCOLORTARGET;
shared texture Gbuffer7RT: RENDERCOLORTARGET;
shared texture Gbuffer8RT: RENDERCOLORTARGET;

#if (ALBEDO_MAP_ENABLE && ALBEDO_MAP_IN_SCREEN_MAP) || (EMISSIVE_MAP_ENABLE && EMISSIVE_MAP_IN_SCREEN_MAP)
    shared texture2D DummyScreenTex : RENDERCOLORTARGET;
#endif

#if USE_CUSTOM_MATERIAL && ALBEDO_MAP_ENABLE && ALBEDO_MAP_IN_TEXTURE != 1
    #if ALBEDO_MAP_IN_SCREEN_MAP == 0
        #if ALBEDO_MAP_ANIMATION_ENABLE
            texture AlbedoMap : ANIMATEDTEXTURE <string ResourceName = ALBEDO_MAP_FILE; float speed=ALBEDO_MAP_ANIMATION_ENABLE;>;
        #else
            texture AlbedoMap<string ResourceName = ALBEDO_MAP_FILE;>; 
        #endif
    #endif
    sampler AlbedoMapSamp = sampler_state
    {
#if ALBEDO_MAP_IN_SCREEN_MAP
        texture = DummyScreenTex;
#else
        texture = AlbedoMap;
#endif
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && ALPHA_MAP_ENABLE && ALPHA_MAP_IN_TEXTURE != 1
    texture2D AlphaMap<string ResourceName = ALPHA_MAP_FILE;>; 
    sampler AlphaMapSamp = sampler_state
    {
        texture = AlphaMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && NORMAL_MAP_ENABLE && NORMAL_MAP_IN_SPHEREMAP != 1
    texture2D NormalMap<string ResourceName = NORMAL_MAP_FILE;>; 
    sampler NormalMapSamp = sampler_state
    {
        texture = NormalMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && NORMAL_MAP_SUB_ENABLE
    texture2D NormalSubMap<string ResourceName = NORMAL_MAP_SUB_FILE;>; 
    sampler NormalSubMapSamp = sampler_state
    {
        texture = NormalSubMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && SMOOTHNESS_MAP_ENABLE && SMOOTHNESS_MAP_IN_TOONMAP != 1
    texture2D SmoothnessMap<string ResourceName = SMOOTHNESS_MAP_FILE;>; 
    sampler SmoothnessMapSamp = sampler_state
    {
        texture = SmoothnessMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && METALNESS_MAP_ENABLE && METALNESS_MAP_IN_TOONMAP != 1
    texture2D MetalnessMap<string ResourceName = METALNESS_MAP_FILE;>; 
    sampler MetalnessMapSamp = sampler_state
    {
        texture = MetalnessMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && SSS_MAP_ENABLE && SSS_ENABLE
    texture2D TransmittanceMap<string ResourceName = SSS_MAP_FILE;>; 
    sampler TransmittanceMapSamp = sampler_state
    {
        texture = TransmittanceMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && CURVATURE_MAP_ENABLE && SSS_ENABLE
    texture2D CurvatureMap<string ResourceName = CURVATURE_MAP_FILE;>; 
    sampler CurvatureMapSamp = sampler_state
    {
        texture = CurvatureMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && MELANIN_MAP_ENABLE
    texture2D MelaninMap<string ResourceName = MELANIN_MAP_FILE;>; 
    sampler MelaninMapSamp = sampler_state
    {
        texture = MelaninMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && EMISSIVE_ENABLE && EMISSIVE_MAP_ENABLE && EMISSIVE_MAP_IN_TEXTURE != 1
    #if EMISSIVE_MAP_IN_SCREEN_MAP == 0
        #if EMISSIVE_MAP_ANIMATION_ENABLE
            texture EmissiveMap : ANIMATEDTEXTURE <string ResourceName = EMISSIVE_MAP_FILE; float speed=EMISSIVE_MAP_ANIMATION_SPEED;>;
        #else
            texture EmissiveMap<string ResourceName = EMISSIVE_MAP_FILE;>; 
        #endif
    #endif
    sampler EmissiveMapSamp = sampler_state
    {
#if EMISSIVE_MAP_IN_SCREEN_MAP
        texture = DummyScreenTex;
#else
        texture = EmissiveMap;
#endif
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if USE_CUSTOM_MATERIAL && PARALLAX_MAP_ENABLE
    texture2D ParallaxMap<string ResourceName = PARALLAX_MAP_FILE;>; 
    sampler ParallaxMapSamp = sampler_state
    {
        texture = ParallaxMap;
        MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
        MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
        ADDRESSU = WRAP; ADDRESSV = WRAP;
    };
#endif

#if ALBEDO_MAP_IN_TEXTURE || ALPHA_MAP_IN_TEXTURE || EMISSIVE_MAP_IN_TEXTURE
texture DiffuseMap: MATERIALTEXTURE;
sampler DiffuseMapSamp = sampler_state
{
    texture = <DiffuseMap>;
    MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
    MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
    ADDRESSU = WRAP; ADDRESSV = WRAP;
};
#endif

#if NORMAL_MAP_IN_SPHEREMAP
texture SphereMap : MATERIALSPHEREMAP;
sampler SphereMapSamp = sampler_state {
    texture = <SphereMap>;
    MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
    MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
    ADDRESSU = WRAP; ADDRESSV = WRAP;
};
#endif

#if SMOOTHNESS_MAP_IN_TOONMAP || METALNESS_MAP_IN_TOONMAP || MELANIN_MAP_IN_TOONMAP
texture ToonMap : MATERIALTOONTEXTURE;
sampler ToonMapSamp = sampler_state {
    texture = <ToonMap>;
    MAXANISOTROPY = TEXTURE_ANISOTROPY_LEVEL;
    MINFILTER = TEXTURE_FILTER; MAGFILTER = TEXTURE_FILTER; MIPFILTER = TEXTURE_MIP_FILTER;
    ADDRESSU = WRAP; ADDRESSV = WRAP;
};
#endif

struct MaterialParam
{
    float3 normal;
    float3 albedo;
    float3 specular;
    float3 transmittance;
    float3 emissive;
    float emissiveIntensity;
    float smoothness;
    float curvature;
    float alpha;
    int lightModel;
};

struct GbufferParam
{
    float4 buffer1 : COLOR0;
    float4 buffer2 : COLOR1;
    float4 buffer3 : COLOR2;
    float4 buffer4 : COLOR3;
};

float3 EncodeNormal(float3 normal)
{
    float p = sqrt(-normal.z * 8 + 8);
    float2 enc = normal.xy / p + 0.5f;
    float2 enc255 = enc * 255;
    float2 residual = floor(frac(enc255) * 16);
    return float3(floor(enc255), residual.x * 16 + residual.y) / 255;
}

float luminance(float3 rgb)
{
    const float3 lumfact = float3(0.2126f, 0.7152f, 0.0722f);
    return dot(rgb, lumfact);
}

float3 srgb2linear(float3 rgb)
{
    const float ALPHA = 0.055f;
    return rgb < 0.04045f ? rgb / 12.92f : pow((abs(rgb) + ALPHA) / (1 + ALPHA), 2.4f);
}

float4 srgb2linear(float4 c)
{
    return float4(srgb2linear(c.rgb), c.a);
}

float3 rgb2ycbcr(float3 col)
{
    float3 encode;
    encode.x = dot(float3(0.299, 0.587, 0.114),   col.rgb);
    encode.y = dot(float3(-0.1687, -0.3312, 0.5), col.rgb);
    encode.z = dot(float3(0.5, -0.4186, -0.0813), col.rgb);
    return half3(encode.x, encode.y * MIDPOINT_8_BIT + MIDPOINT_8_BIT, encode.z * MIDPOINT_8_BIT + MIDPOINT_8_BIT);
}

float3 ComputeTangentBinormalNormal(float3 N, float3 viewdir, float2 coord, float3 tangentNormal)
{
    float3 dp1 = ddx(viewdir);
    float3 dp2 = ddy(viewdir);
    float2 duv1 = ddx(coord);
    float2 duv2 = ddy(coord);

    float3x3 M = float3x3(dp1, dp2, N);
    float2x3 I = float2x3(cross(M[1], M[2]), cross(M[2], M[0]));
    float3 T = mul(float2(duv1.x, duv2.x), I);
    float3 B = mul(float2(duv1.y, duv2.y), I);
    
    float scaleT = 1.0f / (dot(T, T) + 1e-6);
    float scaleB = 1.0f / (dot(B, B) + 1e-6);
    
    float3x3 tbnTransform;
    tbnTransform[0] = normalize(T * scaleT);
    tbnTransform[1] = normalize(B * scaleB);
    tbnTransform[2] = N;

    return normalize(mul(tangentNormal, tbnTransform));
}

#if PARALLAX_MAP_ENABLE
float3 ParallaxOcclusionDirection(float3 normal, float3 worldPos, float2 coord)
{
    float3 viewNormal = mul(normal, (float3x3)matView);
    float3 viewPosition = mul(float4(worldPos, 1), matView).xyz;
    float3 viewdir = normalize(-viewPosition);

    float3 dp1 = ddx(viewPosition);
    float3 dp2 = ddy(viewPosition);
    
    float2 duv1 = ddx(coord);
    float2 duv2 = ddy(coord);

    float3x3 M = float3x3(dp1, dp2, viewNormal);
    float2x3 I = float2x3(cross(M[1], M[2]), cross(M[2], M[0]));
       
    float2 proj = mul(I, viewdir) / dot(dp1, I[0]);
    
    float3 direction;
    direction.xy = duv1 * proj.x + duv2 * proj.y;
    direction.z = dot(viewNormal, viewdir);
    
    return direction;
}

float2 ParallaxOcclusionMap(sampler heightMap, float2 coord, float3 V, int numSteps, float parallaxScale)
{
    float step = 1.0 / numSteps;
    float2 delta = parallaxScale * V.xy / (-V.z * numSteps);

    float curLayerHeight = 0;
    float curHeight = tex2D(heightMap, coord).r;
    float2 curTexcoord = coord;

    [unroll]
    for (int i = 0; i < numSteps; i++)
    {
        if (curHeight <= curLayerHeight)
            break;
            
        curLayerHeight += step; 
        curTexcoord -= delta;
        curHeight = tex2D(heightMap, curTexcoord).r;
    }
    
    float2 deltaTexcoord = delta *= 0.5;
    float deltaHeight = step *= 0.5;

    curTexcoord += deltaTexcoord;
    curLayerHeight -= deltaHeight;

    [unroll]
    for (int j = 0; j < 5; j++)
    {
        deltaTexcoord *= 0.5;
        deltaHeight *= 0.5;

        curHeight = tex2D(heightMap, curTexcoord).r;

        if (curHeight > curLayerHeight)
        {
            curTexcoord -= deltaTexcoord;
            curLayerHeight += deltaHeight;
        }
        else
        {
            curTexcoord += deltaTexcoord;
            curLayerHeight -= deltaHeight;
        }
    }

    return curTexcoord;
}
#endif

float SmoothnessToRoughness(float smoothness)
{
    return (1.0f - smoothness) * (1.0f - smoothness);
}

float RoughnessToSmoothness(float roughness)
{
    return 1.0f - sqrt(roughness);
}

float ShininessToSmoothness(float spec)
{
    return RoughnessToSmoothness(sqrt(2.0 / (spec + 2)));
}

GbufferParam EncodeGbuffer(MaterialParam material, float linearDepth)
{
    GbufferParam gbuffer;
    gbuffer.buffer1.xyz = material.albedo;
    gbuffer.buffer1.w = material.smoothness;

    material.normal = mul(material.normal, (float3x3)matView);
    material.normal = normalize(material.normal);
    
    gbuffer.buffer2.xyz = EncodeNormal(material.normal);
    gbuffer.buffer2.w = 0;

    gbuffer.buffer3.xyz = rgb2ycbcr(material.specular);
    gbuffer.buffer3.w = 0;

    if (material.lightModel == LIGHTINGMODEL_TRANSMITTANCE)
    {
        float scatteringAmount = frac(material.curvature);
        gbuffer.buffer2.w = scatteringAmount > 0.01 ? material.curvature / TWO_BITS_EXTRACTION_FACTOR : 0;
        
        material.transmittance = rgb2ycbcr(material.transmittance);
        gbuffer.buffer3.yz = material.transmittance.gb;
        gbuffer.buffer3.w = material.transmittance.r * MAX_FRACTIONAL_8_BIT;
    }
    else if (material.lightModel == LIGHTINGMODEL_EMISSIVE)
    {
        material.emissive = rgb2ycbcr(material.emissive);
        gbuffer.buffer3.yz = material.emissive.gb;
        gbuffer.buffer3.w = material.emissive.r * MAX_FRACTIONAL_8_BIT;
    }

    gbuffer.buffer3.w = ((float)material.lightModel + gbuffer.buffer3.w) / TWO_BITS_EXTRACTION_FACTOR;
    gbuffer.buffer4 = float4(linearDepth, material.emissiveIntensity, linearDepth, material.alpha);
    
    return gbuffer;
}

float3 ColorSynthesis(float3 diffuse, float m)
{
    float3 melanin = diffuse * luminance(diffuse);
    return diffuse * lerp(1.0, melanin, m);
}

float GetAlpha(float2 coord, bool use_texture)
{
#if SKYBOX_ENABLE
    return 1;
#elif USE_CUSTOM_MATERIAL
    #if ALPHA_MAP_ENABLE
        #if ALPHA_MAP_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
    
        #if ALPHA_MAP_IN_TEXTURE
            if (use_texture)
            {
                #if ALPHA_MAP_SWIZZLE == 0
                    return tex2D(DiffuseMapSamp, coord).r * MaterialDiffuse.a;
                #elif ALPHA_MAP_SWIZZLE == 1
                    return tex2D(DiffuseMapSamp, coord).g * MaterialDiffuse.a;
                #elif ALPHA_MAP_SWIZZLE == 2
                    return tex2D(DiffuseMapSamp, coord).b * MaterialDiffuse.a;
                #elif ALPHA_MAP_SWIZZLE == 3
                    return tex2D(DiffuseMapSamp, coord).a * MaterialDiffuse.a;
                #else
                    return tex2D(DiffuseMapSamp, coord).a * MaterialDiffuse.a;
                #endif            
            }
            else
            {
                return MaterialDiffuse.a;
            }
        #else
            #if ALPHA_MAP_SWIZZLE == 0
                return tex2D(AlphaMapSamp, coord * alphaMapLoopNum).r;
            #elif ALPHA_MAP_SWIZZLE == 1
                return tex2D(AlphaMapSamp, coord * alphaMapLoopNum).g;
            #elif ALPHA_MAP_SWIZZLE == 2
                return tex2D(AlphaMapSamp, coord * alphaMapLoopNum).b;
            #elif ALPHA_MAP_SWIZZLE == 3
                return tex2D(AlphaMapSamp, coord * alphaMapLoopNum).a;
            #else
                return tex2D(AlphaMapSamp, coord * alphaMapLoopNum).a;
            #endif
        #endif
    #else
        return alpha;
    #endif
#else
    return use_texture ? tex2D(DiffuseMapSamp, coord).a * MaterialDiffuse.a : MaterialDiffuse.a;
#endif
}

float3 GetAlbedo(float2 uv, bool use_texture)
{
#if USE_CUSTOM_MATERIAL
    #if ALBEDO_MAP_ENABLE
        #if ALBEDO_MAP_UV_FLIP
            uv.y = 1 - uv.y;
        #endif
        #if ALBEDO_MAP_IN_TEXTURE
            float3 albedoColor = MaterialDiffuse.rgb;
            if (use_texture)
            {
                albedoColor = tex2D(DiffuseMapSamp, uv * albedoMapLoopNum).rgb;
                albedoColor = lerp(1.0, albedoColor * TextureMulValue.rgb + TextureAddValue.rgb, TextureMulValue.a + TextureAddValue.a);
            #if ALBEDO_MAP_APPLY_DIFFUSE
                albedoColor *= MaterialDiffuse.rgb;
            #endif
            }
        #else
            float3 albedoColor = tex2D(AlbedoMapSamp, uv * albedoMapLoopNum).rgb;
            albedoColor = lerp(1.0, albedoColor * TextureMulValue.rgb + TextureAddValue.rgb, TextureMulValue.a + TextureAddValue.a);
            #if ALBEDO_MAP_APPLY_DIFFUSE
                albedoColor *= MaterialDiffuse.rgb;
            #endif
        #endif
        
        albedoColor = srgb2linear(albedoColor);
        
        #if ALBEDO_MAP_APPLY_COLOR
            albedoColor *= albedo.rgb;
        #endif
        
        #if ALBEDO_APPLY_MORPH_COLOR
            albedoColor *= float3(MorphRed, MorphGreen, MorphBlue);
        #endif
        
        return albedoColor;
    #elif ALBEDO_APPLY_MORPH_COLOR
        return float3(MorphRed, MorphGreen, MorphBlue);
    #else
        return albedo.rgb;
    #endif
#else
    float3 diffuse = MaterialDiffuse.rgb;
    
    if (use_texture)
    {
        float4 TexColor = tex2D(DiffuseMapSamp, uv);
        TexColor.rgb = lerp(1, TexColor * TextureMulValue + TextureAddValue, TextureMulValue.a + TextureAddValue.a).rgb;
        diffuse *= TexColor.rgb;
    }
    
    return srgb2linear(diffuse);
#endif
}

float3 GetEmissiveColor(float2 uv, bool use_texture)
{
#if USE_CUSTOM_MATERIAL && EMISSIVE_ENABLE
    #if EMISSIVE_MAP_ENABLE || EMISSIVE_USE_ALBEDO       
        #if EMISSIVE_USE_ALBEDO
            float3 emissiveColor = GetAlbedo(uv, use_texture);
        #else
            #if EMISSIVE_MAP_UV_FLIP
                uv.y = 1 - uv.y;
            #endif
            #if EMISSIVE_MAP_IN_TEXTURE
                float3 emissiveColor = srgb2linear(tex2D(DiffuseMapSamp, uv * emissiveMapLoopNum).rgb);
            #else
                float3 emissiveColor = srgb2linear(tex2D(EmissiveMapSamp, uv * emissiveMapLoopNum).rgb);
            #endif
        #endif
        
        #if EMISSIVE_APPLY_COLOR
            emissiveColor *= emissive.rgb;
        #endif
        
        #if EMISSIVE_APPLY_MORPH_COLOR
            emissiveColor *= float3(MorphRed, MorphGreen, MorphBlue);
        #endif
        
        return emissiveColor;
    #elif EMISSIVE_APPLY_MORPH_COLOR
        return float3(MorphRed, MorphGreen, MorphBlue);
    #else
        return emissive.rgb;
    #endif
#else
    return MaterialSpecular * step(0, MaterialPower - 200);
#endif
}

float GetEmissiveIntensity()
{
#if USE_CUSTOM_MATERIAL
    #if EMISSIVE_ENABLE
        #if EMISSIVE_APPLY_MORPH_INTENSITY
            return emissiveIntensity * LightIntensity;
        #else
            return emissiveIntensity;
        #endif
    #else
        return 0;
    #endif
#else
    return max(0, (MaterialPower - 200) * 0.1);
#endif
}

float3 GetNormal(float3 N, float3 viewdir, float2 coord)
{
#if SKYBOX_ENABLE
    return -N;
#endif
#if USE_CUSTOM_MATERIAL && (NORMAL_MAP_ENABLE || NORMAL_MAP_SUB_ENABLE)
    #if NORMAL_MAP_UV_FLIP
        coord.y = 1 - coord.y;
    #endif

    #if NORMAL_MAP_IN_SPHEREMAP
        float3 tangentNormal = tex2D(SphereMapSamp, coord * normalMapLoopNum).rgb * 2 - 1;
        #if NORMAL_MAP_IS_COMPRESSED
            tangentNormal.z = sqrt(1.0 - tangentNormal.x * tangentNormal.x - tangentNormal.y * tangentNormal.y);
        #endif
        tangentNormal.rg *= normalMapScale;
        tangentNormal = normalize(tangentNormal);
    #elif NORMAL_MAP_SUB_ENABLE && !NORMAL_MAP_ENABLE
        float3 tangentNormal = tex2D(NormalSubMapSamp, coord * normalMapSubLoopNum).rgb * 2 - 1;
        #if NORMAL_MAP_SUB_IS_COMPRESSED
            tangentNormal.z = sqrt(1.0 - tangentNormal.x * tangentNormal.x - tangentNormal.y * tangentNormal.y);
        #endif
        tangentNormal.rg *= normalMapSubScale;
        tangentNormal = normalize(tangentNormal);
    #else    
        float3 tangentNormal = tex2D(NormalMapSamp, coord * normalMapLoopNum).rgb * 2 - 1;
        #if NORMAL_MAP_IS_COMPRESSED
            tangentNormal.z = sqrt(1.0 - tangentNormal.x * tangentNormal.x - tangentNormal.y * tangentNormal.y);
        #endif
        tangentNormal.rg *= normalMapScale;
        tangentNormal = normalize(tangentNormal);
    #endif
       
    #if NORMAL_MAP_UV_FLIP
        coord.y = 1 - coord.y;
    #endif
    
    #if NORMAL_MAP_ENABLE && NORMAL_MAP_SUB_ENABLE
        #if NORMAL_MAP_SUB_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
    
        float3 tangentSubNormal = tex2D(NormalSubMapSamp, coord * normalMapSubLoopNum).rgb * 2 - 1;
        #if NORMAL_MAP_SUB_IS_COMPRESSED
            tangentSubNormal.z = sqrt(1.0 - tangentSubNormal.x * tangentSubNormal.x - tangentSubNormal.y * tangentSubNormal.y);
        #endif
        tangentSubNormal.rg *= normalMapSubScale;
        tangentSubNormal = normalize(tangentSubNormal);
        
        tangentNormal.rg += tangentSubNormal.rg;
        tangentNormal = normalize(tangentNormal);
        
        #if NORMAL_MAP_SUB_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
    #endif
    
    return ComputeTangentBinormalNormal(N, viewdir, coord, tangentNormal);
#else
    return N;
#endif
}

float GetSmoothness(float2 coord)
{
#if USE_CUSTOM_MATERIAL
    #if SMOOTHNESS_MAP_ENABLE
        #if SMOOTHNESS_MAP_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
    
        #if SMOOTHNESS_MAP_IN_TOONMAP || SMOOTHNESS_MAP_IN_TONEMAP
            #if SMOOTHNESS_MAP_SWIZZLE == 0
                float smoothness = tex2D(ToonMapSamp, coord * smoothnessMapLoopNum).r;
            #elif SMOOTHNESS_MAP_SWIZZLE == 1
                float smoothness = tex2D(ToonMapSamp, coord * smoothnessMapLoopNum).g;
            #elif SMOOTHNESS_MAP_SWIZZLE == 2
                float smoothness = tex2D(ToonMapSamp, coord * smoothnessMapLoopNum).b;
            #elif SMOOTHNESS_MAP_SWIZZLE == 3
                float smoothness = tex2D(ToonMapSamp, coord * smoothnessMapLoopNum).a;
            #else
                float smoothness = tex2D(ToonMapSamp, coord * smoothnessMapLoopNum).r;
            #endif
        #else
            #if SMOOTHNESS_MAP_SWIZZLE == 0
                float smoothness = tex2D(SmoothnessMapSamp, coord * smoothnessMapLoopNum).r;
            #elif SMOOTHNESS_MAP_SWIZZLE == 1
                float smoothness = tex2D(SmoothnessMapSamp, coord * smoothnessMapLoopNum).g;
            #elif SMOOTHNESS_MAP_SWIZZLE == 2
                float smoothness = tex2D(SmoothnessMapSamp, coord * smoothnessMapLoopNum).b;
            #elif SMOOTHNESS_MAP_SWIZZLE == 3
                float smoothness = tex2D(SmoothnessMapSamp, coord * smoothnessMapLoopNum).a;
            #else
                float smoothness = tex2D(SmoothnessMapSamp, coord * smoothnessMapLoopNum).r;
            #endif
        #endif
        
        #if SMOOTHNESS_MAP_IS_ROUGHNESS
            smoothness = RoughnessToSmoothness(smoothness);
        #endif
        
        return smoothness;
    #else       
        return smoothness;
    #endif
#else
    return ShininessToSmoothness(MaterialPower);
#endif
}

float GetMetalness(float2 coord)
{
#if USE_CUSTOM_MATERIAL
    #if METALNESS_MAP_ENABLE
        #if METALNESS_MAP_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
        
        #if METALNESS_MAP_IN_TONEMAP || METALNESS_MAP_IN_TOONMAP
            #if METALNESS_MAP_SWIZZLE == 0
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).r;
            #elif METALNESS_MAP_SWIZZLE == 1
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).g;
            #elif METALNESS_MAP_SWIZZLE == 2
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).b;
            #elif METALNESS_MAP_SWIZZLE == 3
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).a;
            #else
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).r;
            #endif
        #else
            #if METALNESS_MAP_SWIZZLE == 0
                return tex2D(MetalnessMapSamp, coord * metalnessMapLoopNum).r;
            #elif METALNESS_MAP_SWIZZLE == 1
                return tex2D(MetalnessMapSamp, coord * metalnessMapLoopNum).g;
            #elif METALNESS_MAP_SWIZZLE == 2
                return tex2D(MetalnessMapSamp, coord * metalnessMapLoopNum).b;
            #elif METALNESS_MAP_SWIZZLE == 3
                return tex2D(MetalnessMapSamp, coord * metalnessMapLoopNum).a;
            #else
                return tex2D(MetalnessMapSamp, coord * metalnessMapLoopNum).r;
            #endif
        #endif
    #else
        return metalness;
    #endif
#else
    return 0.0;
#endif
}

float3 GetTransmittance(float2 coord)
{
#if SSS_ENABLE && USE_CUSTOM_MATERIAL
    #if SSS_MAP_ENABLE
        #if SSS_MAP_UV_FLIP
            coord.y = 1 - coord.y;
        #endif

        #if SSS_MAP_IS_THICKNESS
            float3 transmittanceMapColor = 1 - tex2D(TransmittanceMapSamp, coord * transmittanceMapLoopNum);
        #else
            float3 transmittanceMapColor = tex2D(TransmittanceMapSamp, coord * transmittanceMapLoopNum).rgb;
            transmittanceMapColor = srgb2linear(transmittanceMapColor);
        #endif

        #if SSS_MAP_APPLY_COLOR
            transmittanceMapColor *= transmittance;
        #endif
        
        return transmittanceMapColor;
    #else
        return transmittance;
    #endif
#else
    return 0.0f;
#endif
}

float3 GetCurvature(float2 coord)
{
#if SSS_ENABLE && USE_CUSTOM_MATERIAL
    #if CURVATURE_MAP_ENABLE
        #if CURVATURE_MAP_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
    
        float transmittanceStrength = tex2D(CurvatureMapSamp, coord * curvatureMapLoopNum).r;
        return floor(curvature), lerp(frac(curvature), 0.999, transmittanceStrength);
    #else
        return curvature;
    #endif
#else
    return 0.0f;
#endif
}

float GetMelanin(float2 coord)
{
#if USE_CUSTOM_MATERIAL
    #if MELANIN_MAP_ENABLE
        #if MELANIN_MAP_UV_FLIP
            coord.y = 1 - coord.y;
        #endif
        
        #if MELANIN_MAP_IN_TOONMAP
            #if MELANIN_MAP_SWIZZLE == 0
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).r;
            #elif MELANIN_MAP_SWIZZLE == 1
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).g;
            #elif MELANIN_MAP_SWIZZLE == 2
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).b;
            #elif MELANIN_MAP_SWIZZLE == 3
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).a;
            #else
                return tex2D(ToonMapSamp, coord * metalnessMapLoopNum).r;
            #endif
        #else
            #if MELANIN_MAP_SWIZZLE == 0
                return tex2D(MelaninMapSamp, coord * metalnessMapLoopNum).r;
            #elif MELANIN_MAP_SWIZZLE == 1
                return tex2D(MelaninMapSamp, coord * metalnessMapLoopNum).g;
            #elif MELANIN_MAP_SWIZZLE == 2
                return tex2D(MelaninMapSamp, coord * metalnessMapLoopNum).b;
            #elif MELANIN_MAP_SWIZZLE == 3
                return tex2D(MelaninMapSamp, coord * metalnessMapLoopNum).a;
            #else
                return tex2D(MelaninMapSamp, coord * metalnessMapLoopNum).r;
            #endif
        #endif
    #else
        return melanin;
    #endif
#else
    return 0.0;
#endif
}

void MaterialVS(
    in float4 Position : POSITION, 
    in float3 Normal : NORMAL, 
    in float2 Texcoord: TEXCOORD0,
    out float3 oNormal   : TEXCOORD0,
    out float3 oTexcoord : TEXCOORD1,
    out float4 oWorldPos : TEXCOORD2,
    out float4 oPosition : POSITION)
{
#if SKYBOX_ENABLE
    oPosition = mul(Position, matWorldViewProject);
    oNormal = normalize(Position.xyz);
#else
    oPosition = mul(Position, matWorldViewProject);
    oNormal = Normal;
#endif
    oTexcoord = float3(Texcoord, oPosition.w);
    oWorldPos = Position;
}

GbufferParam MaterialPS(
    in float3 normal   : TEXCOORD0,
    in float3 coord : TEXCOORD1,
    in float3 worldPos  : TEXCOORD2,
    uniform bool useTexture)
{
    normal = normalize(normal);
    
#if USE_CUSTOM_MATERIAL && PARALLAX_MAP_ENABLE
    #if PARALLAX_MAP_UV_FLIP
        coord.y = 1 - coord.y;
    #endif
    float3 viewdir = ParallaxOcclusionDirection(normal, worldPos, coord.xy);
    coord.xy = ParallaxOcclusionMap(ParallaxMapSamp, coord.xy * parallaxMapLoopNum, viewdir, 15, parallaxMapScale);
    #if PARALLAX_MAP_UV_FLIP
        coord.y = 1 - coord.y;
    #endif
#endif

    float alpha = GetAlpha(coord.xy, useTexture);
    clip(alpha - ALPHA_THRESHOLD);
    
    float3 albedoColor = GetAlbedo(coord.xy, useTexture);
    albedoColor = ColorSynthesis(albedoColor, GetMelanin(coord.xy));
    
    float3 MaterialMetalness = GetMetalness(coord.xy);

    MaterialParam material;
    material.normal = GetNormal(normal, worldPos, coord.xy);
    material.smoothness = GetSmoothness(coord.xy);
    material.albedo = albedoColor * (1.0 - MaterialMetalness);
    material.specular = lerp(metalnessBaseSpecular, albedoColor, MaterialMetalness);
    material.transmittance = GetTransmittance(coord.xy) * (1.0 - MaterialMetalness);
    material.curvature = GetCurvature(coord.xy);
    material.emissive = GetEmissiveColor(coord.xy, useTexture);
    material.emissiveIntensity = GetEmissiveIntensity();
    material.alpha = 1;

#if SSS_ENABLE && USE_CUSTOM_MATERIAL
    #if EMISSIVE_ENABLE
        material.lightModel = any(material.emissive) ? LIGHTINGMODEL_EMISSIVE : LIGHTINGMODEL_TRANSMITTANCE;
    #else
        material.lightModel = LIGHTINGMODEL_TRANSMITTANCE;
    #endif
#elif EMISSIVE_ENABLE && USE_CUSTOM_MATERIAL
    material.lightModel = any(material.emissive) ? LIGHTINGMODEL_EMISSIVE : LIGHTINGMODEL_NORMAL;
#else
    material.lightModel = (material.emissiveIntensity > 0) ? LIGHTINGMODEL_EMISSIVE : LIGHTINGMODEL_NORMAL;
#endif

    return EncodeGbuffer(material, coord.z);
}

GbufferParam Material2PS(
    in float3 normal   : TEXCOORD0,
    in float3 coord : TEXCOORD1,
    in float3 worldPos  : TEXCOORD2,
    uniform bool useTexture)
{
    normal = normalize(normal);
    
#if USE_CUSTOM_MATERIAL && PARALLAX_MAP_ENABLE && PARALLAX_MAP_SUPPORT_ALPHA
    #if PARALLAX_MAP_UV_FLIP
        coord.y = 1 - coord.y;
    #endif
    float3 viewdir = ParallaxOcclusionDirection(normal, worldPos, coord.xy);
    coord.xy = ParallaxOcclusionMap(ParallaxMapSamp, coord.xy * parallaxMapLoopNum, viewdir, 15, parallaxMapScale);
    #if PARALLAX_MAP_UV_FLIP
        coord.y = 1 - coord.y;
    #endif
#endif

    float alpha = GetAlpha(coord.xy, useTexture);
    clip(alpha - 0.01);

    float3 albedoColor = GetAlbedo(coord.xy, useTexture);
    albedoColor = ColorSynthesis(albedoColor, GetMelanin(coord.xy));
   
    float3 MaterialMetalness = GetMetalness(coord.xy);

    MaterialParam material;
    material.normal = GetNormal(normal, worldPos, coord.xy);
    material.smoothness = GetSmoothness(coord.xy);
    material.albedo = albedoColor * (1.0 - MaterialMetalness);
    material.specular = lerp(metalnessBaseSpecular, albedoColor, MaterialMetalness);
    material.transmittance = GetTransmittance(coord.xy) * (1.0 - MaterialMetalness);
    material.curvature = GetCurvature(coord.xy);
    material.emissive = GetEmissiveColor(coord.xy, useTexture);
    material.emissiveIntensity = GetEmissiveIntensity();
    material.alpha = alpha < ALPHA_THRESHOLD ? alpha : 0;

#if SSS_ENABLE && USE_CUSTOM_MATERIAL
    #if EMISSIVE_ENABLE
        material.lightModel = any(material.emissive) ? LIGHTINGMODEL_EMISSIVE : LIGHTINGMODEL_TRANSMITTANCE;
    #else
        material.lightModel = LIGHTINGMODEL_TRANSMITTANCE;
    #endif
#elif EMISSIVE_ENABLE && USE_CUSTOM_MATERIAL
    material.lightModel = any(material.emissive) ? LIGHTINGMODEL_EMISSIVE : LIGHTINGMODEL_NORMAL;
#else
    material.lightModel = (material.emissiveIntensity > 0) ? LIGHTINGMODEL_EMISSIVE : LIGHTINGMODEL_NORMAL;
#endif

    return EncodeGbuffer(material, coord.z);
}

#define OBJECT_TEC(name, mmdpass, tex) \
    technique name < string MMDPass = mmdpass; bool UseTexture = tex; \
    string Script = \
        "RenderColorTarget0=;" \
        "RenderColorTarget1=Gbuffer2RT;" \
        "RenderColorTarget2=Gbuffer3RT;" \
        "RenderColorTarget3=Gbuffer4RT;" \
        "Pass=DrawObject;" \
        "RenderColorTarget0=Gbuffer5RT;" \
        "RenderColorTarget1=Gbuffer6RT;" \
        "RenderColorTarget2=Gbuffer7RT;" \
        "RenderColorTarget3=Gbuffer8RT;" \
        "Pass=DrawAlphaObject;"\
    ; \
    > { \
        pass DrawObject { \
            AlphaTestEnable = false; AlphaBlendEnable = false; \
            VertexShader = compile vs_3_0 MaterialVS(); \
            PixelShader  = compile ps_3_0 MaterialPS(tex); \
        } \
        pass DrawAlphaObject { \
            AlphaTestEnable = false; AlphaBlendEnable = false; \
            VertexShader = compile vs_3_0 MaterialVS(); \
            PixelShader  = compile ps_3_0 Material2PS(tex); \
        } \
    }

OBJECT_TEC(MainTec0, "object", false)
OBJECT_TEC(MainTec1, "object", true)
OBJECT_TEC(MainTec2, "object", false)
OBJECT_TEC(MainTec3, "object", true)
OBJECT_TEC(MainTecBS0, "object_ss", false)
OBJECT_TEC(MainTecBS1, "object_ss", true)
OBJECT_TEC(MainTecBS2, "object_ss", false)
OBJECT_TEC(MainTecBS3, "object_ss", true)

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTech < string MMDPass = "shadow";  > {}
technique ZplotTec < string MMDPass = "zplot"; > {}