#if SSR_QUALITY == 1
#	define SSR_SAMPLER_COUNT 32
#elif SSR_QUALITY == 2
#	define SSR_SAMPLER_COUNT 64
#elif SSR_QUALITY >= 3
#	define SSR_SAMPLER_COUNT 128
#else
#	define SSR_SAMPLER_COUNT 32
#endif

static float SSRScale1 = ((ViewportSize.x * 1.0)   / (mSSRBlurScale * 1.0));
static float SSRScale2 = ((ViewportSize.x * 0.5)   / (mSSRBlurScale * 0.5));
static float SSRScale3 = ((ViewportSize.x * 0.25)  / (mSSRBlurScale * 0.25));
static float SSRScale4 = ((ViewportSize.x * 0.125) / (mSSRBlurScale * 0.125));

static float2 SSROffsetX1 = float2(ViewportOffset2.x * 1, 0.0) * SSRScale1;
static float2 SSROffsetX2 = float2(ViewportOffset2.x * 2, 0.0) * SSRScale2;
static float2 SSROffsetX3 = float2(ViewportOffset2.x * 4, 0.0) * SSRScale3;
static float2 SSROffsetX4 = float2(ViewportOffset2.x * 8, 0.0) * SSRScale4;

static float2 SSROffsetY1 = float2(0.0, ViewportOffset2.y * 1) * SSRScale1;
static float2 SSROffsetY2 = float2(0.0, ViewportOffset2.y * 2) * SSRScale2;
static float2 SSROffsetY3 = float2(0.0, ViewportOffset2.y * 4) * SSRScale3;
static float2 SSROffsetY4 = float2(0.0, ViewportOffset2.y * 8) * SSRScale4;

texture SSRLightX1Map : RENDERCOLORTARGET<float2 ViewportRatio = {1.0, 1.0}; string Format = "A8B8G8R8";>;
texture SSRLightX2Map : RENDERCOLORTARGET<float2 ViewportRatio = {0.5, 0.5}; string Format = "A8B8G8R8";>;
texture SSRLightX3Map : RENDERCOLORTARGET<float2 ViewportRatio = {0.25, 0.25}; string Format = "A8B8G8R8";>;
texture SSRLightX4Map : RENDERCOLORTARGET<float2 ViewportRatio = {0.125, 0.125}; string Format = "A8B8G8R8";>;
texture SSRLightX1MapTemp : RENDERCOLORTARGET<float2 ViewportRatio = {1.0, 1.0}; string Format = "A8B8G8R8";>;
texture SSRLightX2MapTemp : RENDERCOLORTARGET<float2 ViewportRatio = {0.5, 0.5}; string Format = "A8B8G8R8";>;
texture SSRLightX3MapTemp : RENDERCOLORTARGET<float2 ViewportRatio = {0.25, 0.25}; string Format = "A8B8G8R8";>;
texture SSRLightX4MapTemp : RENDERCOLORTARGET<float2 ViewportRatio = {0.125, 0.125}; string Format = "A8B8G8R8";>;

sampler SSRLightX1Samp = sampler_state { texture = <SSRLightX1Map>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX2Samp = sampler_state { texture = <SSRLightX2Map>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX3Samp = sampler_state { texture = <SSRLightX3Map>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX4Samp = sampler_state { texture = <SSRLightX4Map>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX1SampTemp = sampler_state { texture = <SSRLightX1MapTemp>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX2SampTemp = sampler_state { texture = <SSRLightX2MapTemp>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX3SampTemp = sampler_state { texture = <SSRLightX3MapTemp>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };
sampler SSRLightX4SampTemp = sampler_state { texture = <SSRLightX4MapTemp>; MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE; AddressU = CLAMP; AddressV = CLAMP; };

bool TraceScreenSpaceRay(float3 viewPosition, float3 viewReflect, float maxDistance, float jitter, out float2 hitPixel)
{
	float4 startPosition = float4(viewPosition, 1);
	float4 startScreenPos = mul(startPosition, matProject);

	float4 endPosition = float4(viewPosition + viewReflect * maxDistance, 1);
	float4 endScreenPos = mul(endPosition, matProject);

	startScreenPos.xy = startScreenPos.xy * float2(0.5, -0.5) + 0.5 * startScreenPos.w;
	endScreenPos.xy = endScreenPos.xy * float2(0.5, -0.5) + 0.5 * endScreenPos.w;

	float4 deltaScreenPos = endScreenPos - startScreenPos;

	float stepSize = 1.0 / SSR_SAMPLER_COUNT;
	float stepLength = jitter * stepSize + stepSize;

	float bestLen = 0;
	float bestIntervalSize = maxDistance / (SSR_SAMPLER_COUNT * 1.6) * mSSRThreshold;

	for (int i = 0; i < SSR_SAMPLER_COUNT; i += 4)
	{
		float4 sampleLength = stepLength + stepSize * float4(0, 1, 2, 3);

		float4 samplePos1 = startScreenPos + deltaScreenPos * sampleLength.x;
		float4 samplePos2 = startScreenPos + deltaScreenPos * sampleLength.y;
		float4 samplePos3 = startScreenPos + deltaScreenPos * sampleLength.z;
		float4 samplePos4 = startScreenPos + deltaScreenPos * sampleLength.w;
		float4 samplePosZ = float4(samplePos1.w, samplePos2.w, samplePos3.w, samplePos4.w);

		float4 sampleDepth;
		sampleDepth.r = tex2Dproj(Gbuffer8Map, samplePos1).r;
		sampleDepth.g = tex2Dproj(Gbuffer8Map, samplePos2).r;
		sampleDepth.b = tex2Dproj(Gbuffer8Map, samplePos3).r;
		sampleDepth.a = tex2Dproj(Gbuffer8Map, samplePos4).r;

		float4 depthDiff = samplePosZ - abs(sampleDepth);

		bool4 hit = abs(depthDiff) < bestIntervalSize;
		if (any(hit))
		{
			float hitIndex = 3;
			hitIndex = hit[2] ? 2 : hitIndex;
			hitIndex = hit[1] ? 1 : hitIndex;
			hitIndex = hit[0] ? 0 : hitIndex;
			bestLen = stepLength + stepSize * hitIndex;
			break;
		}

		stepLength += stepSize * 4;
	}

	float4 projPos = startScreenPos + deltaScreenPos * bestLen;
	projPos.xy /= projPos.w;

	hitPixel = projPos.xy;

	if (hitPixel.x < 0.0 || hitPixel.x > 1.0 || hitPixel.y < 0.0 || hitPixel.y > 1.0)
	{
		bestLen = 0;
	}

	return bestLen > 0 ? 1 : 0;
}

float4 SSRConeTracingPS(in float4 coord : TEXCOORD0, in float3 viewdir : TEXCOORD1) : COLOR 
{
	float4 MRT0 = tex2Dlod(Gbuffer5Map, float4(coord.xy, 0, 0));
	float4 MRT1 = tex2Dlod(Gbuffer6Map, float4(coord.xy, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer7Map, float4(coord.xy, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer8Map, float4(coord.xy, 0, 0));

	MaterialParam material;
	DecodeGbuffer(MRT0, MRT1, MRT2, MRT3, material);

	float3 test = material.albedo + material.specular;
	clip(sum(test) - 1e-5);

	float3 view = normalize(viewdir);
	float3 viewPosition = view * material.linearDepth / view.z;
	float3 viewReflect = normalize(reflect(-view, material.normal));

	float atten = dot(viewReflect, -view);
	clip(atten - 1e-5);

	float sampleJitter = GetJitterOffset(coord.zw);
	float sampleDistance = mSSRRangeScale;
	sampleDistance *= max(0, mSSRRangeMax - material.linearDepth);
	sampleDistance *= pow5(1 - saturate(dot(view, material.normal)));

	float2 hitPixel = 0;
	bool hitTest = TraceScreenSpaceRay(viewPosition, viewReflect, sampleDistance, sampleJitter, hitPixel);
	if (!hitTest)
		clip(-1);

	float2 boundary = abs(coord.xy * 2 - 1);
	float fadeDiffRcp = 1.0f / max(0.01, 1 - mSSRFadeStart);
	float fadeOnBorder = 1.0f - saturate((boundary.x - mSSRFadeStart) * fadeDiffRcp);
	fadeOnBorder *= 1.0f - saturate((boundary.y - mSSRFadeStart) * fadeDiffRcp);
	fadeOnBorder = smoothstep(0.0f, 1.0f, fadeOnBorder);

	float fadeOnPerpendicular = saturate(atten);
	float fadeOnTotal = fadeOnBorder * fadeOnPerpendicular;

	float4 totalColor = tex2Dlod(ShadingMapPointSamp, float4(hitPixel, 0, 0));
	return float4(totalColor.rgb, 1) * fadeOnTotal * hitTest;
}

float4 SSRGaussionBlurPS(in float4 coord : TEXCOORD0, uniform sampler source, uniform float2 offset) : COLOR
{
	float3 incrementalGaussian = IncrementalGaussianCofficent(4);

	float weight = incrementalGaussian.x;
	float4 color = tex2Dlod(source, float4(coord.xy, 0, 0)) * weight;

	float2 offset0 = coord.xy + offset;
	float2 offset1 = coord.xy - offset;

	[unroll]
	for (int i = 0; i < 4; ++i)
	{
		incrementalGaussian = IncrementalGaussian(incrementalGaussian);

		color += tex2Dlod(source, float4(offset0, 0, 0)) * incrementalGaussian.x;
		color += tex2Dlod(source, float4(offset1, 0, 0)) * incrementalGaussian.x;

		offset0 += offset;
		offset1 -= offset;

		weight += 2 * incrementalGaussian.x;
	}

	return color / weight;
}

float4 SSRFinalCombiePS(in float2 coord : TEXCOORD0, in float3 viewdir : TEXCOORD1) : COLOR
{
	float4 MRT0 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT1 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	MaterialParam material;
	DecodeGbuffer(MRT0, MRT1, MRT2, MRT3, material);

	float gloss = material.smoothness;
	float gloss2 = gloss * gloss;

	float weight = frac(min(gloss2, 0.9999) * 3);

	float4 refl0 = tex2Dlod(SSRLightX1Samp, float4(coord, 0, 0));
	float4 refl1 = tex2Dlod(SSRLightX2Samp, float4(coord, 0, 0));
	float4 refl2 = tex2Dlod(SSRLightX3Samp, float4(coord, 0, 0));
	float4 refl3 = tex2Dlod(SSRLightX4Samp, float4(coord, 0, 0));

	float4 color = 0;

	[branch]
	if (gloss2 > 2.0 / 3.0)
		color = lerp(refl1, refl0, weight * weight);
	else if (gloss2 > 1.0 / 3.0)
		color = lerp(refl2, refl1, weight);
	else
		color = lerp(refl3, refl2, weight);

	float3 N = material.normal;
	float3 V = normalize(viewdir);
	float3 f0 = min(0.02, material.specular);	
	float3 fresnel = EnvironmentSpecularUnreal4(N, V, gloss, f0);

	return float4(color.rgb * fresnel, color.a * luminance(fresnel));
}