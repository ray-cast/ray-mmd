#define SHADINGMODELID_DEFAULT    0
#define SHADINGMODELID_SKIN       1
#define SHADINGMODELID_EMISSIVE   2
#define SHADINGMODELID_HAIR       3
#define SHADINGMODELID_GLASS      4
#define SHADINGMODELID_CLOTH      5
#define SHADINGMODELID_CLEAR_COAT 6
#define SHADINGMODELID_SUBSURFACE 7
#define SHADINGMODELID_MASK       8

#define MAX_FRACTIONAL_8_BIT       (255.0f / 256.0f)
#define TWO_BITS_EXTRACTION_FACTOR (3.0f + MAX_FRACTIONAL_8_BIT)

struct MaterialParam
{
	float3 normal;
	float3 albedo;
	float3 specular;
	float smoothness;
	float alpha;
	float visibility;
	float customDataA;
	float3 customDataB;
	float linearDepth;
	int lightModel;
};

struct GbufferParam
{
	float4 buffer1 : COLOR0;
	float4 buffer2 : COLOR1;
	float4 buffer3 : COLOR2;
	float4 buffer4 : COLOR3;
};

float3 EncodeNormal(float3 normal)
{
	float p = sqrt(-normal.z * 8 + 8);
	float2 enc = normal.xy / p + 0.5f;
	float2 enc255 = enc * 255;
	float2 residual = floor(frac(enc255) * 16);
	return float3(floor(enc255), residual.x * 16 + residual.y) / 255;
}

float3 DecodeNormal(float3 enc)
{
	float nz = floor(enc.z * 255) / 16;
	enc.xy += float2(floor(nz) / 16, frac(nz)) / 255;
	float2 fenc = enc.xy * 4 - 2;
	float f = dot(fenc, fenc);
	float g = sqrt(1 - f / 4);
	float3 normal;
	normal.xy = fenc * g;
	normal.z = f / 2 - 1;
	return normalize(normal);
}

float3 EncodeYcbcr(float3 col)
{
	col = sqrt(col);
	float3 encode;
	encode.x = dot(float3(0.299, 0.587, 0.114),   col.rgb);
	encode.y = dot(float3(-0.1687, -0.3312, 0.5), col.rgb) * MIDPOINT_8_BIT + MIDPOINT_8_BIT;
	encode.z = dot(float3(0.5, -0.4186, -0.0813), col.rgb) * MIDPOINT_8_BIT + MIDPOINT_8_BIT;
	return encode;
}

float3 DecodeYcbcr(float3 YCbCr)
{
	YCbCr = float3(YCbCr.x, YCbCr.y / MIDPOINT_8_BIT - 1, YCbCr.z / MIDPOINT_8_BIT - 1);
	float R = YCbCr.x + 1.402 * YCbCr.z;
	float G = dot(float3( 1, -0.3441, -0.7141 ), YCbCr.xyz);
	float B = YCbCr.x + 1.772 * YCbCr.y;
	return pow2(float3(R, G, B));
}

void DecodeGbuffer(float4 buffer1, float4 buffer2, float4 buffer3, float4 buffer4, out MaterialParam material)
{
	material.albedo = buffer1.xyz;
	material.smoothness = buffer1.w;

	material.normal = DecodeNormal(buffer2.xyz);

	material.customDataA = buffer2.w;
	material.customDataB = 0;

	material.alpha = buffer4.w;

	material.linearDepth = buffer4.x;
	material.visibility = saturate(frac(buffer4.y));
	material.lightModel = (int)floor(buffer4.z);

	if (material.lightModel >= SHADINGMODELID_MASK)
		material.lightModel = SHADINGMODELID_DEFAULT;

	if (material.lightModel != SHADINGMODELID_DEFAULT)
	{
		material.specular = pow2(buffer3.x);
		material.customDataB = DecodeYcbcr(float3(frac(buffer3.w * TWO_BITS_EXTRACTION_FACTOR), buffer3.yz));
		if (material.lightModel == SHADINGMODELID_EMISSIVE)
		{
			material.customDataA = floor(buffer4.y) / 1000.0;
		}
	}
	else
	{
		material.specular = DecodeYcbcr(buffer3.xyz);
	}
}

float3 DecodeGBufferNormal(float4 buffer2)
{
	return DecodeNormal(buffer2.rgb);
}

int DecodeLightModel(float4 buffer4)
{
	int lightModel = (int)floor(abs(buffer4.z));
	return lightModel;
}

float DecodeEmissiveIntensity(float4 buffer4)
{
	return floor(buffer4.y) / 1000.0;
}

float3 DecodeGBufferEmissive(float4 buffer3, float4 buffer4)
{
	int lightModel = DecodeLightModel(buffer4);
	float intensity = DecodeEmissiveIntensity(buffer4);
	float3 emissive = max(0, DecodeYcbcr(float3(frac(buffer3.w * TWO_BITS_EXTRACTION_FACTOR), buffer3.yz)));
	return emissive * intensity * any(lightModel == SHADINGMODELID_EMISSIVE);
}

float4 GetProjConstant()
{
	return float4(2, -2, -1, 1) * float2(matProjectInverse._11, matProjectInverse._22).xyxy / matProjectInverse._43;
}

float3 GetViewPosition(float4 projConstant, float2 coord, float depth)
{
	return float3(coord * projConstant.xy + projConstant.zw, 1) * depth;
}