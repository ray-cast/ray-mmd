float4x4 CreateOrthoProject(float width, float height, float near, float far)
{
	float cx = 2.0f / width;
	float cy = 2.0f / height;
	float cz = 1.0f / (far - near);
	float tz = near / (near - far);

	float4x4 m;
	m[0] = float4(cx,  0,  0, 0);
	m[1] = float4( 0, cy,  0, 0);
	m[2] = float4( 0,  0,  1, 0);
	m[3] = float4( 0,  0,  0, 1);

	return m;
}

float4x4 CreateLightViewMatrix(float3 forward, float3 LightDirection, float LightDistance)
{
	float3 right = cross(float3(0.0f, 0.0f, 1.0f), forward);
	float3 up;

	if (any(right))
	{
		right = normalize(right);
		up = cross(forward, right);
	}
	else
	{
		right = float3(1.0f, 0.0f, 0.0f);
		up = float3(0.0f, 0.0f, -sign(forward.y));
	}

	float3x3 rotation = {right.x, up.x, forward.x,
						 right.y, up.y, forward.y,
						 right.z, up.z, forward.z };

	return float4x4(rotation[0], 0,
					rotation[1], 0,
					rotation[2], 0,
					mul(-LightDirection * LightDistance, rotation), 1);
}

float4 CreateFrustumSplitPoint(float n, float f, float lambda = 0.5)
{
	float4 p = float4(1,2,3,4) / 4;
	float4 p0 = n + (f - n) / n * p;
	float4 p1 = n * pow(f / n, p);
	return lerp(p0, p1, lambda);
}

float4 CreateFrustumFromProjInverse()
{
	float r = dot(float4( 1, 0, 0, 1), matProjectInverse._11_21_31_41);
	float l = dot(float4(-1, 0, 0, 1), matProjectInverse._11_21_31_41);
	float t = dot(float4( 0, 1, 0, 1), matProjectInverse._12_22_32_42);
	float b = dot(float4( 0,-1, 0, 1), matProjectInverse._12_22_32_42);
	return float4(r, l, t, b);
}

float4 CreateLightCropParam(float4x4 matLightViewFromCameraView, float4 frustumInfo, int shadowMapSize, float near, float far)
{
	float4 zfar = float4(far.xxx, 1);
	float4 znear = float4(near.xxx, 1);

	float4 rtn = float4(frustumInfo.xz, 1, 1) * znear;
	float4 rtf = float4(frustumInfo.xz, 1, 1) * zfar;
	float4 lbn = float4(frustumInfo.yw, 1, 1) * znear;
	float4 lbf = float4(frustumInfo.yw, 1, 1) * zfar;

	float4 rbn = float4(rtn.x, lbn.yzw);
	float4 rbf = float4(rtf.x, lbf.yzw);
	float4 ltn = float4(lbn.x, rtn.yzw);
	float4 ltf = float4(lbf.x, rtf.yzw);

	float2 vrtn = mul(rtn, matLightViewFromCameraView).xy;
	float2 vrtf = mul(rtf, matLightViewFromCameraView).xy;
	float2 vlbn = mul(lbn, matLightViewFromCameraView).xy;
	float2 vlbf = mul(lbf, matLightViewFromCameraView).xy;
	float2 vrbn = mul(rbn, matLightViewFromCameraView).xy;
	float2 vrbf = mul(rbf, matLightViewFromCameraView).xy;
	float2 vltn = mul(ltn, matLightViewFromCameraView).xy;
	float2 vltf = mul(ltf, matLightViewFromCameraView).xy;

	float4 minBB = float4(vrtn, vrtf);
	float4 maxBB = minBB;
	minBB = min(float4(vlbn, vlbf), minBB);
	maxBB = max(float4(vlbn, vlbf), maxBB);
	minBB = min(float4(vrbn, vrbf), minBB);
	maxBB = max(float4(vrbn, vrbf), maxBB);
	minBB = min(float4(vltn, vltf), minBB);
	maxBB = max(float4(vltn, vltf), maxBB);

	float4 cropBB = float4(min(minBB.xy, minBB.zw), max(maxBB.xy, maxBB.zw));
	cropBB += (cropBB.xyzw - cropBB.zwxy) * (mPSSMPadding * 2 + 1) / shadowMapSize;
	float4 unit = (cropBB.zwzw - cropBB.xyxy) * 2 / shadowMapSize;
	cropBB = floor(cropBB / unit) * unit;

	// approximate flickering removal
	float quantizer = 64.0f;

	float2 scale = 2.0 / (cropBB.zw - cropBB.xy);
	scale = quantizer / ceil(quantizer / scale);

	float2 offset = -0.5 * (cropBB.xy + cropBB.zw) * scale;
	offset = ceil(offset * shadowMapSize * 0.5f) / (shadowMapSize * 0.5f);

	return float4(scale, offset);
}

float4x4 CreateLightCropParams(float4x4 matLightViewFromCameraView, float shadowMapSize)
{
	const float4 frustumInfo = CreateFrustumFromProjInverse();
	const float4 frustumSplit = CreateFrustumSplitPoint(mPSSMCascadeZMin, mPSSMCascadeZMax, mPSSMCascadeLambda);

	return float4x4(
		CreateLightCropParam(matLightViewFromCameraView, frustumInfo, shadowMapSize, mPSSMCascadeZMin, frustumSplit.x),
		CreateLightCropParam(matLightViewFromCameraView, frustumInfo, shadowMapSize, frustumSplit.x, frustumSplit.y),
		CreateLightCropParam(matLightViewFromCameraView, frustumInfo, shadowMapSize, frustumSplit.y, frustumSplit.z),
		CreateLightCropParam(matLightViewFromCameraView, frustumInfo, shadowMapSize, frustumSplit.z, frustumSplit.w));
}

float CalcEdgeFalloff(float2 coord, int shadowMapSize, float shadowMapOffset)
{
	const float m = shadowMapSize * 0.5 / mPSSMPadding;
	const float a = shadowMapOffset * 1.0 / mPSSMPadding;
	float2 falloff = abs(coord) * (-m * 4.0) + (m - a);
	return saturate(min(falloff.x, falloff.y));
}

float4 CalcCascadePPos(float2 uv, float2 offset, float index, int shadowMapSize)
{
	return float4(uv + 0.25 + offset * 0.5 + (0.5 / shadowMapSize), index, CalcEdgeFalloff(uv, shadowMapSize, 1.0 / shadowMapSize));
}

float ShadowSlopeScaledBias(float depth)
{
	float dx = abs(ddx(depth));
	float dy = abs(ddy(depth));
	float depthSlope = min(dx, dy);
	return depthSlope;
}